
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("Объект") Тогда
		Если Параметры.Объект.ПодписанЭП Тогда
			ЗаполнитьСписокПодписейОдногоОбъекта(Параметры.Объект, Параметры.УникальныйИдентификатор);
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Свойство("Подписи") Тогда
		ЗаполнитьСписокПодписейИзМассива(Параметры.Подписи, Параметры.УникальныйИдентификатор);
	КонецЕсли;
	
	БольшеНеСпрашивать = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьПодпись(Команда)
	
	МассивВозврата = Новый Массив;
	
	Для Каждого Строка Из ТаблицаПодписей Цикл
		Если Строка.Пометка Тогда
			СтруктураВозврата = Новый Структура("АдресПодписи, КомуВыданСертификат, ИмяФайлаПодписи", 
												Строка.АдресПодписи,
												Строка.КомуВыданСертификат,
												Строка.ИмяФайлаПодписи);
			МассивВозврата.Добавить(СтруктураВозврата);
		КонецЕсли;
	КонецЦикла;
	
	Если БольшеНеСпрашивать Тогда
		ЗапомнитьБольшеНеСпрашивать();
		ОбновитьПовторноИспользуемыеЗначения();
	КонецЕсли;
	
	Закрыть(МассивВозврата);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаПодписейАвторПодписи.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаПодписейДатаПодписи.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаПодписейКомментарий.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаПодписей.Неверна");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветОсобогоТекста);

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокПодписейОдногоОбъекта(ОбъектСсылка, УникальныйИдентификаторВызывающейФормы)
	
	ПолноеИмяОбъектаСЭП = ОбъектСсылка.Метаданные().ПолноеИмя();
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
					|	ЭлектронныеПодписи.КомуВыданСертификат КАК КомуВыданСертификат,
					|	ЭлектронныеПодписи.ДатаПодписи КАК ДатаПодписи,
					|	ЭлектронныеПодписи.Комментарий КАК Комментарий,
					|	ЭлектронныеПодписи.Подпись КАК Подпись,
					|	ЭлектронныеПодписи.Отпечаток КАК Отпечаток,
					|	ЭлектронныеПодписи.УстановившийПодпись КАК УстановившийПодпись,
					|	ЭлектронныеПодписи.НомерСтроки КАК НомерСтроки,
					|	ЭлектронныеПодписи.ИмяФайлаПодписи КАК ИмяФайлаПодписи
					|ИЗ
					|	[ПолноеИмяОбъектаСЭП].ЭлектронныеПодписи КАК ЭлектронныеПодписи
					|ГДЕ
					|	ЭлектронныеПодписи.Ссылка = &СсылкаНаОбъект";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "[ПолноеИмяОбъектаСЭП]", ПолноеИмяОбъектаСЭП);
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.Параметры.Вставить("СсылкаНаОбъект", ОбъектСсылка);
	
	ВыборкаЗапроса = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаЗапроса.Следующий() Цикл
		НоваяСтрока = ТаблицаПодписей.Добавить();
		НоваяСтрока.КомуВыданСертификат = ВыборкаЗапроса.КомуВыданСертификат;
		НоваяСтрока.ДатаПодписи = ВыборкаЗапроса.ДатаПодписи;
		НоваяСтрока.Комментарий = ВыборкаЗапроса.Комментарий;
		НоваяСтрока.Отпечаток = ВыборкаЗапроса.Отпечаток;
		НоваяСтрока.УстановившийПодпись = ВыборкаЗапроса.УстановившийПодпись;
		НоваяСтрока.ИмяФайлаПодписи = ВыборкаЗапроса.ИмяФайлаПодписи;
		НоваяСтрока.Неверна = Ложь;
		НоваяСтрока.ИндексКартинки = -1;
		НоваяСтрока.Пометка = Истина;
		НоваяСтрока.АдресПодписи = ПоместитьВоВременноеХранилище(ВыборкаЗапроса.Подпись.Получить(), УникальныйИдентификаторВызывающейФормы);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокПодписейИзМассива(Подписи, УникальныйИдентификаторВызывающейФормы)
	
	Для Каждого Подпись Из Подписи Цикл
		НоваяСтрока = ТаблицаПодписей.Добавить();
		
		НоваяСтрока.КомуВыданСертификат = Подпись.КомуВыданСертификат;
		НоваяСтрока.ДатаПодписи = Подпись.ДатаПодписи;
		НоваяСтрока.Комментарий = Подпись.Комментарий;
		НоваяСтрока.Отпечаток = Подпись.Отпечаток;
		НоваяСтрока.УстановившийПодпись = Подпись.УстановившийПодпись;
		НоваяСтрока.ИмяФайлаПодписи = Подпись.ИмяФайлаПодписи;
		НоваяСтрока.Неверна = Ложь;
		НоваяСтрока.ИндексКартинки = -1;
		НоваяСтрока.Пометка = Истина;
		НоваяСтрока.АдресПодписи = ПоместитьВоВременноеХранилище(Подпись.Подпись, УникальныйИдентификаторВызывающейФормы);
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗапомнитьБольшеНеСпрашивать()
	ЧастьНастроек = Новый Структура("ДействияПриСохраненииСЭП", Перечисления.ДействияПриСохраненииСЭП.СохранятьВсеПодписи);
	ЭлектроннаяПодпись.СохранитьПерсональныеНастройки(ЧастьНастроек);
	ОбновитьПовторноИспользуемыеЗначения();
КонецПроцедуры

#КонецОбласти
