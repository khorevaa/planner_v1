#Область ПрограммныйИнтерфейс

// Добавляет команды переключения цвета на форму
// На форме необходимо реализовать следующие изменения:
//	1. Добавить группу "Горизонтальная" для добавления палитры цветов текущего цвета и палитры
//	2. Добавить на форму надпись "ДекорацияТекущийЦвет". В нее устанавливается значение текущего цвета
//	3. Добавить обработку команды при нажатии на кнопку цвета (см. КомандаДействие)
//		Используется одна обработка. Для определения цвета в обработку передается команда с именем по шаблону
//		"Цвет_УникальныйИдентификатор". При этом символ "-" в идентификаторе заменен на "_". Это идентификатор
//		для получения ссылки на элемент справочника "ЦветоваяГаммаПланировщика"
//	4. После декорации рекомендуется добавить группу для размещения команд по цветам (см. ИмяГруппы)
//	5. В обработке команд устанавливать признак модифицированности или записывать значение цвета
//
// Параметры:
//	Форма - Тип: УправляемаяФорма
//	ТекущийЦвет - Тип: СправочникСсылка.ЦветоваяГаммаПланировщика
//		Значение текущего цвета. Устанавливается в элемент "ДекорацияТекущийЦвет" который должен 
//	ИмяГруппы - Тип: Строка.
//	КомандаДействие - Тип: Строка
//	Использование - Тип: Булево
//
Процедура ДобавитьПалитруЦветовНаФорму(Форма, ТекущийЦвет, ИмяГруппы, КомандаДействие, Использование) Экспорт
	
	ДанныеПоЦветам = ПолучитьДанныеЦветовойГаммы(Использование);
	Если ДанныеПоЦветам.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущийЦвет) Тогда
		РеквизитыЦвета	= ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТекущийЦвет, "ЦветФонаКрасный, ЦветФонаЗеленый, ЦветФонаСиний");
		Форма.Элементы.ДекорацияТекущийЦвет.ЦветФона = Новый Цвет(РеквизитыЦвета.ЦветФонаКрасный, РеквизитыЦвета.ЦветФонаЗеленый, РеквизитыЦвета.ЦветФонаСиний);
	КонецЕсли;
	
	ЭлементРодитель = Форма.Элементы[ИмяГруппы];
	
	Для Каждого СтрокаТаблицы Из ДанныеПоЦветам Цикл
		Идентификатор	= СтрЗаменить(Строка(СтрокаТаблицы.Ссылка.УникальныйИдентификатор()), "-", "_");
		ИмяКоманды		= "Цвет_" + Идентификатор;
		
		// КомандаФормы
		Команда = Форма.Команды.Добавить(ИмяКоманды);
		Команда.Действие	= КомандаДействие;
		Команда.Заголовок	= "";
		Команда.ИзменяетСохраняемыеДанные = Истина;
		Команда.Отображение	= ОтображениеКнопки.Картинка;
		Команда.Подсказка	= "";
		
		// ЭлементФормы
		ЭлементФормы = Форма.Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), ЭлементРодитель);
		ЭлементФормы.Вид			= ВидКнопкиФормы.ОбычнаяКнопка;
		ЭлементФормы.Высота			= 1;
		ЭлементФормы.Заголовок		= "";
		ЭлементФормы.ИмяКоманды		= ИмяКоманды;
		ЭлементФормы.Отображение	= ОтображениеКнопки.Картинка;
		ЭлементФормы.ЦветФона		= Новый Цвет(СтрокаТаблицы.ЦветФонаКрасный, СтрокаТаблицы.ЦветФонаЗеленый, СтрокаТаблицы.ЦветФонаСиний);
		ЭлементФормы.Ширина			= 3;
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьВидыИспользованияЦветов() Экспорт
	
	Соответствие = Новый Соответствие;
	Соответствие.Вставить(0, "Календари");
	Соответствие.Вставить(1, "События");
	Возврат Соответствие;
	
КонецФункции

Процедура ОбновитьСписокЦветовСправочника() Экспорт
	
	ЦветаКалендари = Новый ТаблицаЗначений;
	ЦветаКалендари.Колонки.Добавить("ЦветФона"	, Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(7)));
	ЦветаКалендари.Колонки.Добавить("ЦветТекста", Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(7)));
	
	ВставитьСтрокуВТаблицуЦветов(ЦветаКалендари, "#ac725e", "#ffffff");
	ВставитьСтрокуВТаблицуЦветов(ЦветаКалендари, "#d06b64", "#ffffff");
	ВставитьСтрокуВТаблицуЦветов(ЦветаКалендари, "#f83a22", "#ffffff");
	ВставитьСтрокуВТаблицуЦветов(ЦветаКалендари, "#ff7537", "#1d1d1d");
	ВставитьСтрокуВТаблицуЦветов(ЦветаКалендари, "#ffad46", "#1d1d1d");
	ВставитьСтрокуВТаблицуЦветов(ЦветаКалендари, "#16a765", "#ffffff");
	ВставитьСтрокуВТаблицуЦветов(ЦветаКалендари, "#7bd148", "#1d1d1d");
	ВставитьСтрокуВТаблицуЦветов(ЦветаКалендари, "#b3dc6c", "#1d1d1d");
	ВставитьСтрокуВТаблицуЦветов(ЦветаКалендари, "#fbe983", "#1d1d1d");
	ВставитьСтрокуВТаблицуЦветов(ЦветаКалендари, "#fad165", "#1d1d1d");
	ВставитьСтрокуВТаблицуЦветов(ЦветаКалендари, "#9fe1e7", "#1d1d1d");
	ВставитьСтрокуВТаблицуЦветов(ЦветаКалендари, "#9fc6e7", "#1d1d1d");
	ВставитьСтрокуВТаблицуЦветов(ЦветаКалендари, "#4986e7", "#ffffff");
	ВставитьСтрокуВТаблицуЦветов(ЦветаКалендари, "#9a9cff", "#1d1d1d");
	ВставитьСтрокуВТаблицуЦветов(ЦветаКалендари, "#b99aff", "#1d1d1d");
	ВставитьСтрокуВТаблицуЦветов(ЦветаКалендари, "#cca6ac", "#1d1d1d");
	ВставитьСтрокуВТаблицуЦветов(ЦветаКалендари, "#f691b2", "#1d1d1d");
	ВставитьСтрокуВТаблицуЦветов(ЦветаКалендари, "#cd74e6", "#ffffff");
	ВставитьСтрокуВТаблицуЦветов(ЦветаКалендари, "#fa573c", "#ffffff");
	
	ЦветаСобытия = Новый ТаблицаЗначений;
	ЦветаСобытия.Колонки.Добавить("ЦветФона"	, Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(7)));
	ЦветаСобытия.Колонки.Добавить("ЦветТекста"	, Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(7)));
	
	ВставитьСтрокуВТаблицуЦветов(ЦветаСобытия, "#a4bdfc", "#1d1d1d");
 	ВставитьСтрокуВТаблицуЦветов(ЦветаСобытия, "#7ae7bf", "#1d1d1d");
	ВставитьСтрокуВТаблицуЦветов(ЦветаСобытия, "#dbadff", "#1d1d1d");
	ВставитьСтрокуВТаблицуЦветов(ЦветаСобытия, "#ff887c", "#1d1d1d");
	ВставитьСтрокуВТаблицуЦветов(ЦветаСобытия, "#fbd75b", "#1d1d1d");
	ВставитьСтрокуВТаблицуЦветов(ЦветаСобытия, "#ffb878", "#1d1d1d");
	ВставитьСтрокуВТаблицуЦветов(ЦветаСобытия, "#46d6db", "#1d1d1d");
	ВставитьСтрокуВТаблицуЦветов(ЦветаСобытия, "#e1e1e1", "#1d1d1d");
	ВставитьСтрокуВТаблицуЦветов(ЦветаСобытия, "#5484ed", "#ffffff");
	ВставитьСтрокуВТаблицуЦветов(ЦветаСобытия, "#51b749", "#ffffff");
	ВставитьСтрокуВТаблицуЦветов(ЦветаСобытия, "#dc2127", "#ffffff");

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаЦветов.ЦветФона КАК ЦветФона,
	|	ТаблицаЦветов.ЦветТекста КАК ЦветТекста
	|ПОМЕСТИТЬ ТаблицаЦветов
	|ИЗ
	|	&ТаблицаЦветов КАК ТаблицаЦветов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЦветФона,
	|	ЦветТекста
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЦветаКалендарей.ЦветФона,
	|	ЦветаКалендарей.ЦветТекста,
	|	ЦветоваяГаммаПланировщика.Ссылка
	|ИЗ
	|	ТаблицаЦветов КАК ЦветаКалендарей
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЦветоваяГаммаПланировщика КАК ЦветоваяГаммаПланировщика
	|		ПО ЦветаКалендарей.ЦветФона = ЦветоваяГаммаПланировщика.ЦветФонаHex
	|			И ЦветаКалендарей.ЦветТекста = ЦветоваяГаммаПланировщика.ЦветТекстаHex
	|			И (ЦветоваяГаммаПланировщика.Использование = &Использование)
	|ГДЕ
	|	ЦветоваяГаммаПланировщика.Ссылка ЕСТЬ NULL ";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ТаблицаЦветов", ЦветаКалендари);
	Запрос.УстановитьПараметр("Использование", 0);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЦветФона	= ПланировщикКлиентСервер.HexВЦвет(Выборка.ЦветФона);
		ЦветТекста	= ПланировщикКлиентСервер.HexВЦвет(Выборка.ЦветТекста);
		
		ЭлементОбъект = Справочники.ЦветоваяГаммаПланировщика.СоздатьЭлемент();
		ЭлементОбъект.УстановитьНовыйКод();
		ЭлементОбъект.Использование 	= 0;
		ЭлементОбъект.ЦветФонаHex		= Выборка.ЦветФона;
		ЭлементОбъект.ЦветФонаКрасный	= ЦветФона.Красный;	
		ЭлементОбъект.ЦветФонаЗеленый	= ЦветФона.Зеленый;
		ЭлементОбъект.ЦветФонаСиний		= ЦветФона.Синий;
		ЭлементОбъект.ЦветТекстаHex		= Выборка.ЦветТекста;
		ЭлементОбъект.ЦветТекстаКрасный	= ЦветТекста.Красный;
		ЭлементОбъект.ЦветТекстаЗеленый	= ЦветТекста.Зеленый;
		ЭлементОбъект.ЦветТекстаСиний	= ЦветТекста.Синий;
		ЭлементОбъект.Записать();
	КонецЦикла;  
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ТаблицаЦветов", ЦветаСобытия);
	Запрос.УстановитьПараметр("Использование", 1);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЦветФона	= ПланировщикКлиентСервер.HexВЦвет(Выборка.ЦветФона);
		ЦветТекста	= ПланировщикКлиентСервер.HexВЦвет(Выборка.ЦветТекста);
		
		ЭлементОбъект = Справочники.ЦветоваяГаммаПланировщика.СоздатьЭлемент();
		ЭлементОбъект.УстановитьНовыйКод();
		ЭлементОбъект.Использование 	= 1;
		ЭлементОбъект.ЦветФонаHex		= Выборка.ЦветФона;
		ЭлементОбъект.ЦветФонаКрасный	= ЦветФона.Красный;	
		ЭлементОбъект.ЦветФонаЗеленый	= ЦветФона.Зеленый;
		ЭлементОбъект.ЦветФонаСиний		= ЦветФона.Синий;
		ЭлементОбъект.ЦветТекстаHex		= Выборка.ЦветТекста;
		ЭлементОбъект.ЦветТекстаКрасный	= ЦветТекста.Красный;
		ЭлементОбъект.ЦветТекстаЗеленый	= ЦветТекста.Зеленый;
		ЭлементОбъект.ЦветТекстаСиний	= ЦветТекста.Синий;
		ЭлементОбъект.Записать();
	КонецЦикла;  
	
	Запрос = Новый Запрос("ВЫБРАТЬ * ИЗ Справочник.ЦветоваяГаммаПланировщика");
	Таб = Запрос.Выполнить().Выгрузить();
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

Процедура ВыполнитьЗагрузкуСправочникаЦветоваяГаммаПоУмолчанию() Экспорт
	
	ЭлементОбъект = Справочники.ЦветоваяГаммаПланировщика.Серый.ПолучитьОбъект();
	ЭлементОбъект.ЦветФонаHex		= "#c2c2c2";
	ЭлементОбъект.ЦветФонаКрасный	= 194;	
	ЭлементОбъект.ЦветФонаЗеленый	= 194;
	ЭлементОбъект.ЦветФонаСиний		= 194;
	ЭлементОбъект.ЦветТекстаHex		= "#1d1d1d";
	ЭлементОбъект.ЦветТекстаКрасный	= 29;
	ЭлементОбъект.ЦветТекстаЗеленый	= 29;
	ЭлементОбъект.ЦветТекстаСиний	= 29;
	ЭлементОбъект.Записать();
	
	ОбновитьСписокЦветовСправочника();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьДанныеЦветовойГаммы(Использование)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Использование", Использование);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЦветоваяГаммаПланировщика.ЦветФонаHex,
	|	ЦветоваяГаммаПланировщика.ЦветФонаКрасный,
	|	ЦветоваяГаммаПланировщика.ЦветФонаСиний,
	|	ЦветоваяГаммаПланировщика.ЦветФонаЗеленый,
	|	ЦветоваяГаммаПланировщика.ЦветТекстаHex,
	|	ЦветоваяГаммаПланировщика.ЦветТекстаКрасный,
	|	ЦветоваяГаммаПланировщика.ЦветТекстаСиний,
	|	ЦветоваяГаммаПланировщика.ЦветТекстаЗеленый,
	|	ЦветоваяГаммаПланировщика.Ссылка
	|ИЗ
	|	Справочник.ЦветоваяГаммаПланировщика КАК ЦветоваяГаммаПланировщика
	|ГДЕ
	|	НЕ ЦветоваяГаммаПланировщика.ПометкаУдаления
	|	И ЦветоваяГаммаПланировщика.Использование = &Использование
	|	И НЕ ЦветоваяГаммаПланировщика.НеОтображатьНаФорме
	|	И НЕ ЦветоваяГаммаПланировщика.Предопределенный
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЦветоваяГаммаПланировщика.Код";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Процедура ВставитьСтрокуВТаблицуЦветов(ТаблицаДанных, ЦветФона, ЦветТекста)
	НоваяСтрока = ТаблицаДанных.Добавить();
	НоваяСтрока.ЦветФона	= ЦветФона;
	НоваяСтрока.ЦветТекста	= ЦветТекста;
КонецПроцедуры

#КонецОбласти
