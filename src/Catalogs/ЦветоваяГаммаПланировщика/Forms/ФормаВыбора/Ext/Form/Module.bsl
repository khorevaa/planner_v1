&НаСервере
Процедура ЗаполнитьЦветаНаФорме()
	ПоследнийЭлемент = 0;
	Для Каждого КлючИЗначение Из СоответствиеЦветов Цикл
		ПоследнийЭлемент = Макс(ПоследнийЭлемент, Число(Сред(КлючИЗначение.Ключ, 2)));
		
		Настройка		= КлючИЗначение.Значение;
		ЗначениеЦвет	= Новый Цвет(Настройка.Красный, Настройка.Зеленый, Настройка.Синий);
		
		Элементы[КлючИЗначение.Ключ].ЦветФона	= ЗначениеЦвет;
		Элементы[КлючИЗначение.Ключ].ЦветРамки	= ЗначениеЦвет;
	КонецЦикла;
	
	Для Индекс = ПоследнийЭлемент По 23 Цикл
		НомерУдаления = "_" + Формат(Индекс+1, "ЧГ=");
		Элементы[НомерУдаления].Видимость = Ложь;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПараметрыФормы = ЭтаФорма.Параметры;
	Если ПараметрыФормы.Свойство("Отбор") Тогда
		Использование = ?(ПараметрыФормы.Отбор.Свойство("Использование"), ПараметрыФормы.Отбор.Использование, 0);
	Иначе 
		Использование = 0;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Использование", Использование);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 24
	|	ЦветаВидовСобытий.Ссылка,
	|	ЦветаВидовСобытий.ВерсияДанных,
	|	ЦветаВидовСобытий.ПометкаУдаления,
	|	ЦветаВидовСобытий.Предопределенный,
	|	ЦветаВидовСобытий.Код КАК Код,
	|	ЦветаВидовСобытий.Красный,
	|	ЦветаВидовСобытий.Зеленый,
	|	ЦветаВидовСобытий.Синий,
	|	ЦветаВидовСобытий.ЗначениеHex,
	|	0 КАК ПолеПорядка
	//|	ИнтеграцияСGoogle.GoogleId,
	//|	ВЫБОР
	//|		КОГДА ИнтеграцияСGoogle.GoogleId <> """"
	//|			ТОГДА 1
	//|		ИНАЧЕ 0
	//|	КОНЕЦ КАК ПолеПорядка
	|ИЗ
	|	Справочник.ЦветоваяГаммаПланировщика КАК ЦветаВидовСобытий
	//|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеИнтеграцииСGoogle КАК ИнтеграцияСGoogle
	//|		ПО ЦветаВидовСобытий.Ссылка = ИнтеграцияСGoogle.Объект
	|ГДЕ
	|	ЦветаВидовСобытий.Использование = &Использование
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПолеПорядка УБЫВ,
	|	Код";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Соответствие = Новый Соответствие;
	Счетчик	= 0;
	
	Пока Выборка.Следующий() Цикл
		Счетчик = Счетчик + 1;
		
		СтруктураЦвета = Новый Структура("Ссылка,Красный,Зеленый,Синий,Значение,GoogleId,ЗначениеHex"); 
		ЗаполнитьЗначенияСвойств(СтруктураЦвета, Выборка);
		Соответствие.Вставить("_" + Строка(Счетчик), СтруктураЦвета);
	КонецЦикла;
	
	СоответствиеЦветов = Новый ФиксированноеСоответствие(Соответствие);
	
	Если Соответствие.Количество() = 0 Тогда
		Отказ = Истина;
	КонецЕсли;
	
	ЗаполнитьЦветаНаФорме();
КонецПроцедуры

&НаКлиенте
Процедура ВыборЦвета(Команда)
	Ссылка = СоответствиеЦветов.Получить(Команда.Имя).Ссылка;
	ЭтаФорма.ОповеститьОВыборе(Ссылка);
КонецПроцедуры
