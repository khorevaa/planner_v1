#Область УправлениеФормой

&НаКлиенте
Процедура УстановитьВидимость()
	ВидимостьИнтервал = (ПовторПериодичность = 1 ИЛИ (ПовторПериодичность >= 5 И ПовторПериодичность <= 7));
	
	Элементы.ПовторИнтервал.Видимость	= ВидимостьИнтервал;
	
	Элементы.ГруппаДниНедели.Видимость	= (ПовторПериодичность = 5);
	Элементы.ГруппаДниМесяца.Видимость	= (ПовторПериодичность = 6);
	
	Элементы.ПовторКоличество.ТолькоПросмотр	= (ПовторВидОкончания = 2);
	Элементы.ПовторОкончание.ТолькоПросмотр		= (ПовторВидОкончания = 1);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗаголовки()
	Если ПовторПериодичность = 1 Тогда
		ТекстЗаголовок = "дн."
	ИначеЕсли ПовторПериодичность = 5 Тогда
		ТекстЗаголовок = "нед.";
	ИначеЕсли ПовторПериодичность = 6 Тогда
		ТекстЗаголовок = "мес.";
	ИначеЕсли ПовторПериодичность = 7 Тогда
		ТекстЗаголовок = "г.";
	Иначе 
		ТекстЗаголовок = "";
	КонецЕсли;
	
	Элементы.НадписьШагИнтервала.Заголовок = ТекстЗаголовок;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПовторПериодичностьПриИзменении(Элемент)
	Если ПовторПериодичность >= 2 И ПовторПериодичность <= 4 Тогда
		ПовторИнтервал		= 0;
		ПовторДниПовторения = "";
	ИначеЕсли ПовторПериодичность = 6 Тогда
		ДеньМесяцаДеньНедели = 1;
	КонецЕсли;
	
	УстановитьВидимость();
	УстановитьЗаголовки();
	ОбновитьПредставлениеПовторения();
КонецПроцедуры

&НаКлиенте
Процедура ПовторИнтервалПриИзменении(Элемент)
	ОбновитьПредставлениеПовторения();
КонецПроцедуры

&НаКлиенте
Процедура ПовторИнтервалНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Если ПовторПериодичность = 1 Тогда
		Начало	= 1;
		Конец	= 30;
	ИначеЕсли ПовторПериодичность = 5 Тогда
		Начало	= 1;
		Конец	= 30;
	ИначеЕсли ПовторПериодичность = 6 Тогда
		Начало	= 1;
		Конец	= 12;
	ИначеЕсли ПовторПериодичность = 7 Тогда
		Начало	= 1;
		Конец	= 30;
	Иначе 
		Начало	= 0;
		Конец	= 0;
	КонецЕсли;
	
	СписокВыбора = Новый СписокЗначений;
	СписокВыбора.Очистить();
	Для Индекс = Начало По Конец Цикл
		СписокВыбора.Добавить(Индекс);
	КонецЦикла;
	
	ДанныеВыбора = СписокВыбора;
	
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ПовторИнтервалОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ОбновитьПредставлениеПовторения();
КонецПроцедуры

&НаКлиенте
Процедура ПовторДниНеделиПриИзменении(Элемент)
	ОбновитьПредставлениеПовторения();
КонецПроцедуры

&НаКлиенте
Процедура ПовторДниМесяцаПриИзменении(Элемент)
	ОбновитьПредставлениеПовторения();
КонецПроцедуры

&НаКлиенте
Процедура ПовторНачалоПриИзменении(Элемент)
	УстановитьВидимость();
	ОбновитьПредставлениеПовторения();
КонецПроцедуры

&НаКлиенте
Процедура ПовторВидОкончанияПриИзменении(Элемент)
	Если ПовторВидОкончания = 0 Тогда
		ПовторОкончание		= Дата(1, 1, 1);
		ПовторКоличество	= 0;
	ИначеЕсли ПовторВидОкончания = 1 Тогда
		ПовторОкончание = Дата(1, 1, 1);
	ИначеЕсли ПовторВидОкончания = 2 Тогда
		ПовторКоличество = 0;
	КонецЕсли;
	
	УстановитьВидимость();
	ОбновитьПредставлениеПовторения();
КонецПроцедуры

&НаКлиенте
Процедура ПовторКоличествоПриИзменении(Элемент)
	ОбновитьПредставлениеПовторения();
КонецПроцедуры

&НаКлиенте
Процедура ПовторОкончаниеПриИзменении(Элемент)
	ОбновитьПредставлениеПовторения();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если НЕ Параметры.Свойство("Объект") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект,
		Параметры.Объект,
		"ПовторПредставление, ПовторВидОкончания, ПовторДниПовторения, ПовторИнтервал, ПовторКоличество, ПовторНачало,
		|ПовторОкончание, ПовторПериодичность, НачалоСобытия, ОкончаниеСобытия");
	
	Если Параметры.ТолькоПросмотр Тогда
		ЭтотОбъект.ТолькоПросмотр = Истина;
		Элементы.ФормаГотово.Видимость = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если ПовторПериодичность = 5 Тогда
		Соответствие = Новый Соответствие;
		Соответствие.Вставить("1","пн");
		Соответствие.Вставить("2","вт");
		Соответствие.Вставить("3","ср");
		Соответствие.Вставить("4","чт");
		Соответствие.Вставить("5","пт");
		Соответствие.Вставить("6","сб");
		Соответствие.Вставить("7","вс");
	ИначеЕсли ПовторПериодичность = 6 Тогда
		Соответствие = Новый Соответствие;
		Соответствие.Вставить("1", 1);
		Соответствие.Вставить("2", 2);
	Иначе 
		Соответствие = Неопределено;
	КонецЕсли;
	
	Если ТипЗнч(Соответствие) = Тип("Соответствие") Тогда
		Массив = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ПовторДниПовторения, ",");
		
		Для Каждого ЗначениеСтроки Из Массив Цикл
			ИмяРеквизита		= ?(ПовторПериодичность = 6, "ДеньМесяцаДеньНедели", Соответствие.Получить(ЗначениеСтроки));
			ЗначениеРеквизита	= ?(ПовторПериодичность = 6, Соответствие.Получить(ЗначениеСтроки), Истина);
			
			ЭтотОбъект[ИмяРеквизита] = ЗначениеРеквизита;
		КонецЦикла;
	КонецЕсли;
	
	УстановитьВидимость();
	УстановитьЗаголовки();
	ОбновитьПредставлениеПовторения();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	Если НЕ ЭтотОбъект.Модифицированность Тогда
		Отмена(Команда);
		Возврат;
	КонецЕсли;
	
	// Дни повторения
	ПовторДниПовторения = "";
	Если ПовторПериодичность = 5 Тогда
		Структура = Новый Структура("пн, вт, ср, чт, пт, сб, вс", "1", "2", "3", "4", "5", "6", "7");
		Для Каждого КлючИЗначение Из Структура Цикл
			Ключ		= КлючИЗначение.Ключ;
			Значение	= КлючИЗначение.Значение;
			Если НЕ ЭтотОбъект[Ключ] Тогда
				Продолжить;
			КонецЕсли;
			
			ПовторДниПовторения = ПовторДниПовторения + ?(ПустаяСтрока(ПовторДниПовторения), "", ",") + Значение;
		КонецЦикла;
	ИначеЕсли ПовторПериодичность = 6 Тогда
		ПовторДниПовторения = Строка(ЭтотОбъект.ДеньМесяцаДеньНедели);
	КонецЕсли;
	
	Структура = Новый Структура("ПовторПредставление, ПовторПериодичность, ПовторНачало, ПовторОкончание, ПовторИнтервал, ПовторКоличество, ПовторВидОкончания, ПовторДниПовторения, НачалоСобытия, ОкончаниеСобытия");
	
	Если НачалоДня(ПовторНачало) <> НачалоДня(НачалоСобытия) Тогда
		Разница = ОкончаниеСобытия - НачалоСобытия;
		
		НачалоСобытия		= НачалоДня(ПовторНачало) + (НачалоСобытия - НачалоДня(НачалоСобытия));
		ОкончаниеСобытия	= НачалоСобытия + Разница;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Структура, ЭтотОбъект);
	ЭтотОбъект.Закрыть(Структура);
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	ЭтотОбъект.Закрыть(Неопределено);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьПредставлениеПовторения()
	// Дни повторения
	ПовторДниПовторения = "";
	Если ПовторПериодичность = 5 Тогда
		Структура = Новый Структура("пн, вт, ср, чт, пт, сб, вс", "1", "2", "3", "4", "5", "6", "7");
		Для Каждого КлючИЗначение Из Структура Цикл
			Ключ		= КлючИЗначение.Ключ;
			Значение	= КлючИЗначение.Значение;
			Если НЕ ЭтотОбъект[Ключ] Тогда
				Продолжить;
			КонецЕсли;
			
			ПовторДниПовторения = ПовторДниПовторения + ?(ПустаяСтрока(ПовторДниПовторения), "", ",") + Значение;
		КонецЦикла;
	ИначеЕсли ПовторПериодичность = 6 Тогда
		ПовторДниПовторения = Строка(ЭтотОбъект.ДеньМесяцаДеньНедели);
	КонецЕсли;
	
	СтруктураПовтор = ПланировщикДанныеКлиентСервер.ПолучитьСтруктуруПовторения();
	ЗаполнитьЗначенияСвойств(СтруктураПовтор, ЭтотОбъект);
	ПовторПредставление = ПланировщикДанныеКлиентСервер.ПолучитьПредставлениеПовторения(СтруктураПовтор);
КонецПроцедуры

#КонецОбласти
