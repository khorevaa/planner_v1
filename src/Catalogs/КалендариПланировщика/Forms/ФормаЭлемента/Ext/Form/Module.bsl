#Область УправлениеФормой

&НаСервере
Процедура ДобавитьКомандыПереключенияЦветовНаСервере()
	Справочники.ЦветоваяГаммаПланировщика.ДобавитьПалитруЦветовНаФорму(ЭтотОбъект, Объект.ЦветФона, "ГруппаКомандыЦветФона", "ЦветФонаКалендаря", 0);	
КонецПроцедуры

&НаСервере
Процедура ИнициализацияФормы()
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимость(Форма, ИмяРеквизита = "")
	
	Объект = Форма.Объект;
	
	Если ИмяРеквизита = "КалендарьGoogle" ИЛИ ПустаяСтрока(ИмяРеквизита) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы,
			"КалендарьGoogle", "Видимость", Объект.СинхронизацияСGoogle);
	КонецЕсли;
	Если ИмяРеквизита = "СоздатьКалендарьВGoogle" ИЛИ ПустаяСтрока(ИмяРеквизита) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы,
			"СоздатьКалендарьВGoogle", "Видимость", Объект.СинхронизацияСGoogle);
	КонецЕсли;
	Если ИмяРеквизита = "Префикс" ИЛИ ПустаяСтрока(ИмяРеквизита) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы,
			"Префикс", "Видимость", Объект.ИспользоватьПрефикс);
	КонецЕсли;
	Если ИмяРеквизита = "НачалоРабочегоВремени" ИЛИ ПустаяСтрока(ИмяРеквизита) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы,
			"НачалоРабочегоВремени", "Видимость", Объект.ИспользоватьРабочееВремя);
	КонецЕсли;
	Если ИмяРеквизита = "ОкончаниеРабочегоВремени" ИЛИ ПустаяСтрока(ИмяРеквизита) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы,
			"ОкончаниеРабочегоВремени", "Видимость", Объект.ИспользоватьРабочееВремя);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИспользоватьПрефиксПриИзменении(Элемент)
	Если НЕ Объект.ИспользоватьПрефикс Тогда
		Объект.Префикс = "";
	КонецЕсли;
	
	УстановитьВидимость(ЭтотОбъект, "Префикс");
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьРабочееВремяПриИзменении(Элемент)
	Если НЕ Объект.ИспользоватьРабочееВремя Тогда
		Объект.НачалоРабочегоВремени	= 0;
		Объект.ОкончаниеРабочегоВремени	= 24;
	КонецЕсли;
	
	УстановитьВидимость(ЭтотОбъект, "НачалоРабочегоВремени");
	УстановитьВидимость(ЭтотОбъект,	"ОкончаниеРабочегоВремени");
КонецПроцедуры

&НаКлиенте
Процедура НачалоРабочегоВремениРегулирование(Элемент, Направление, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Объект.НачалоРабочегоВремени = Объект.НачалоРабочегоВремени + Направление;
	Если Объект.НачалоРабочегоВремени < 0 Тогда
		Объект.НачалоРабочегоВремени = 0;
	ИначеЕсли Объект.НачалоРабочегоВремени > 23 Тогда
		Объект.НачалоРабочегоВремени = 23;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОкончаниеРабочегоВремениРегулирование(Элемент, Направление, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Объект.ОкончаниеРабочегоВремени = Объект.ОкончаниеРабочегоВремени + Направление;
	Если Объект.ОкончаниеРабочегоВремени < 1 Тогда
		Объект.ОкончаниеРабочегоВремени = 1;
	ИначеЕсли Объект.ОкончаниеРабочегоВремени > 24 Тогда
		Объект.ОкончаниеРабочегоВремени = 24;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая() Тогда
		ИнициализацияФормы();
	КонецЕсли;
	
	ДобавитьКомандыПереключенияЦветовНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ИнициализацияФормы();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьВидимость(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ПриОбновленииКалендаря" И Параметр = Объект.Ссылка Тогда
		ЭтотОбъект.Прочитать();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("ПослеЗаписиКалендариПланировщика", Новый Структура("Владелец, Ссылка", Объект.Владелец, Объект.Ссылка), Объект.Ссылка);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте 
Процедура ЦветФонаКалендаря(Команда)
	
	ИдентификаторКоманды	= Сред(Команда.Имя, 6);
	ИдентификаторЦвета		= Новый УникальныйИдентификатор(СтрЗаменить(ИдентификаторКоманды, "_", "-"));
	
	ДанныеЦвета = ПолучитьЦветПоИдентификаторуНаСервере(ИдентификаторЦвета);
	
	Объект.ЦветФона = ДанныеЦвета.Ссылка;
	Элементы.ДекорацияТекущийЦвет.ЦветФона = Новый Цвет(ДанныеЦвета.Красный, ДанныеЦвета.Зеленый, ДанныеЦвета.Синий);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста 
Функция ПолучитьЦветПоИдентификаторуНаСервере(ИдентификаторЦвета)
	
	ДанныеЦвета = Новый Структура("Ссылка, Красный, Зеленый, Синий",
		Неопределено, 29, 29, 29);
	
	ЦветСсылка = Справочники.ЦветоваяГаммаПланировщика.ПолучитьСсылку(ИдентификаторЦвета);
	Если ОбщегоНазначения.СсылкаСуществует(ЦветСсылка) Тогда
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЦветСсылка, "ЦветФонаКрасный, ЦветФонаЗеленый, ЦветФонаСиний");
		
		ДанныеЦвета.Ссылка	= ЦветСсылка;
		ДанныеЦвета.Красный	= Реквизиты.ЦветФонаКрасный;
		ДанныеЦвета.Зеленый	= Реквизиты.ЦветФонаЗеленый;
		ДанныеЦвета.Синий	= Реквизиты.ЦветФонаСиний;
	КонецЕсли;
	
	Возврат ДанныеЦвета; 

	
КонецФункции

#КонецОбласти
