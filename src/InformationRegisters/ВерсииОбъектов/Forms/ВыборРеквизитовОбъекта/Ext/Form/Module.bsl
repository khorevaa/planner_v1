
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	КоллекцияРеквизитовОбъекта = РеквизитыОбъекта.ПолучитьЭлементы();
	
	ОбъектМетаданных = Параметры.Ссылка.Метаданные();
	Для Каждого ОписаниеРеквизита Из ОбъектМетаданных.Реквизиты Цикл
		Реквизит = КоллекцияРеквизитовОбъекта.Добавить();
		ЗаполнитьЗначенияСвойств(Реквизит, ОписаниеРеквизита);
		Реквизит.Пометка = Параметры.Отбор.Найти(ОписаниеРеквизита.Имя) <> Неопределено;
		Если ПустаяСтрока(Реквизит.Синоним) Тогда 
			Реквизит.Синоним = Реквизит.Имя;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ОписаниеТабличнойЧасти Из ОбъектМетаданных.ТабличныеЧасти Цикл
		ТабличнаяЧасть = КоллекцияРеквизитовОбъекта.Добавить();
		ЗаполнитьЗначенияСвойств(ТабличнаяЧасть, ОписаниеТабличнойЧасти);
		Для Каждого ОписаниеРеквизита Из ОписаниеТабличнойЧасти.Реквизиты Цикл
			Реквизит = ТабличнаяЧасть.ПолучитьЭлементы().Добавить();
			ЗаполнитьЗначенияСвойств(Реквизит, ОписаниеРеквизита);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	УстановитьСнятьФлажки(Истина);
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	УстановитьСнятьФлажки(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура РеквизитыОбъектаПометкаПриИзменении(Элемент)
	Пометка = Элементы.РеквизитыОбъекта.ТекущиеДанные.Пометка;
	Для Каждого ВыделеннаяСтрока Из Элементы.РеквизитыОбъекта.ВыделенныеСтроки Цикл 
		Реквизит = Элементы.РеквизитыОбъекта.ДанныеСтроки(ВыделеннаяСтрока);
		Реквизит.Пометка = Пометка;
		Для Каждого ПодчиненныйРеквизит Из Реквизит.ПолучитьЭлементы() Цикл
			ПодчиненныйРеквизит.Пометка = Пометка;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСнятьФлажки(Пометка)
	Для Каждого Реквизит Из РеквизитыОбъекта.ПолучитьЭлементы() Цикл 
		Реквизит.Пометка = Пометка;
		Для Каждого ПодчиненныйРеквизит Из Реквизит.ПолучитьЭлементы() Цикл
			ПодчиненныйРеквизит.Пометка = Пометка;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры


&НаКлиенте
Процедура Выбрать(Команда)
	Результат = Новый Структура;
	Результат.Вставить("ВыбранныеРеквизиты", ВыбранныеРеквизиты(РеквизитыОбъекта.ПолучитьЭлементы()));
	Результат.Вставить("ПредставлениеВыбранных", ПредставлениеВыбранныхРеквизитов());

	Закрыть(Результат);
КонецПроцедуры

&НаКлиенте
Функция ВыбранныеРеквизиты(КоллекцияРеквизитов)
	Результат = Новый Массив;
	
	Для Каждого Реквизит Из КоллекцияРеквизитов Цикл
		ПодчиненныеРеквизиты = Реквизит.ПолучитьЭлементы();
		Если ПодчиненныеРеквизиты.Количество() > 0 Тогда
			СписокВыбранных = ВыбранныеРеквизиты(ПодчиненныеРеквизиты);
			Для Каждого ПодчиненныйРеквизит Из СписокВыбранных Цикл
				Результат.Добавить(Реквизит.Имя + "." + ПодчиненныйРеквизит);
			КонецЦикла;
		ИначеЕсли Реквизит.Пометка Тогда
			Результат.Добавить(Реквизит.Имя);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Функция ПредставлениеВыбранныхРеквизитов()
	Возврат СтроковыеФункцииКлиентСервер.СтрокаИзМассиваПодстрок(СинонимыВыбранныхРеквизитов(РеквизитыОбъекта.ПолучитьЭлементы()), ", ");
КонецФункции

&НаКлиенте
Функция СинонимыВыбранныхРеквизитов(КоллекцияРеквизитов)
	Результат = Новый Массив;
	
	Для Каждого Реквизит Из КоллекцияРеквизитов Цикл
		ПодчиненныеРеквизиты = Реквизит.ПолучитьЭлементы();
		Если ПодчиненныеРеквизиты.Количество() > 0 Тогда
			СписокВыбранных = ВыбранныеРеквизиты(ПодчиненныеРеквизиты);
			Для Каждого ПодчиненныйРеквизит Из СписокВыбранных Цикл
				Результат.Добавить(Реквизит.Синоним + "." + ПодчиненныйРеквизит);
			КонецЦикла;
		ИначеЕсли Реквизит.Пометка Тогда
			Результат.Добавить(Реквизит.Синоним);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
КонецФункции


