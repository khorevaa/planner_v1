#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// При записи контролируются курсы подчиненных валют
//
Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.Свойство("ОтключитьКонтрольПодчиненныхВалют") Тогда
		
		ДополнительныеСвойства.Вставить("ЗависимыеВалюты", Новый Соответствие);
		
		Если Количество() > 0 Тогда
			ЗагрузитьКурсыПодчиненныхВалют();
		Иначе
			УдалитьКурсыПодчиненныхВалют();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Находит все зависимые валюты и изменяет их курс
//
Процедура ЗагрузитьКурсыПодчиненныхВалют()
	
	Для Каждого ЗаписьВалютыВладельца Из ЭтотОбъект Цикл
		
		ЗависимыеВалюты = РаботаСКурсамиВалют.ПолучитьСписокЗависимыхВалют(ЗаписьВалютыВладельца.Валюта, ДополнительныеСвойства);
		Для Каждого СтрокаТаблицы Из ЗависимыеВалюты Цикл
			
			НаборЗаписей = РегистрыСведений.КурсыВалют.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Валюта.Установить(СтрокаТаблицы.Ссылка, Истина);
			НаборЗаписей.Отбор.Период.Установить(ЗаписьВалютыВладельца.Период, Истина);
			
			ЗаписьКурсовВалют = НаборЗаписей.Добавить();
			ЗаписьКурсовВалют.Валюта = СтрокаТаблицы.Ссылка;
			ЗаписьКурсовВалют.Период = ЗаписьВалютыВладельца.Период;
			Если СтрокаТаблицы.СпособУстановкиКурса = Перечисления.СпособыУстановкиКурсаВалюты.НаценкаНаКурсДругойВалюты Тогда
				ЗаписьКурсовВалют.Курс = ЗаписьВалютыВладельца.Курс + ЗаписьВалютыВладельца.Курс * СтрокаТаблицы.Наценка / 100;
				ЗаписьКурсовВалют.Кратность = ЗаписьВалютыВладельца.Кратность;
			Иначе // по формуле
				Курс = КурсВалютыПоФормуле(СтрокаТаблицы.Ссылка, СтрокаТаблицы.ФормулаРасчетаКурса, ЗаписьВалютыВладельца.Период);
				Если Курс <> Неопределено Тогда
					ЗаписьКурсовВалют.Курс = Курс;
					ЗаписьКурсовВалют.Кратность = 1;
				КонецЕсли;
			КонецЕсли;
				
			НаборЗаписей.ДополнительныеСвойства.Вставить("ОтключитьКонтрольПодчиненныхВалют", Истина);
			
			Если ЗаписьКурсовВалют.Курс > 0 Тогда
				НаборЗаписей.Записать();
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

// Очищает курсы зависимых валют
//
Процедура УдалитьКурсыПодчиненныхВалют()
	
	ВалютаВладелец = Отбор.Валюта.Значение;
	Период = Отбор.Период;
	
	ЗависимыеВалюты = РаботаСКурсамиВалют.ПолучитьСписокЗависимыхВалют(ВалютаВладелец, ДополнительныеСвойства);
	Для Каждого СтрокаТаблицы Из ЗависимыеВалюты Цикл
		НаборЗаписей = РегистрыСведений.КурсыВалют.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Валюта.Установить(СтрокаТаблицы.Ссылка, Истина);
		НаборЗаписей.Отбор.Период.Установить(Период, Истина);
		НаборЗаписей.ДополнительныеСвойства.Вставить("ОтключитьКонтрольПодчиненныхВалют", Истина);
		НаборЗаписей.Записать();
	КонецЦикла;
	
КонецПроцедуры

Функция КурсВалютыПоФормуле(Валюта, Формула, Период)
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Валюты.Наименование КАК СимвольныйКод,
	|	КурсыВалютСрезПоследних.Курс / КурсыВалютСрезПоследних.Кратность КАК Курс
	|ИЗ
	|	Справочник.Валюты КАК Валюты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Период, ) КАК КурсыВалютСрезПоследних
	|		ПО КурсыВалютСрезПоследних.Валюта = Валюты.Ссылка
	|ГДЕ
	|	Валюты.СпособУстановкиКурса <> ЗНАЧЕНИЕ(Перечисление.СпособыУстановкиКурсаВалюты.НаценкаНаКурсДругойВалюты)
	|	И Валюты.СпособУстановкиКурса <> ЗНАЧЕНИЕ(Перечисление.СпособыУстановкиКурсаВалюты.РасчетПоФормуле)";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Период", Период);
	Выражение = Формула;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Выражение = СтрЗаменить(Выражение, Выборка.СимвольныйКод, Формат(Выборка.Курс, "ЧРД=.; ЧГ=0"));
	КонецЦикла;
	
	Попытка
		Результат = РаботаВБезопасномРежиме.ВычислитьВБезопасномРежиме(Выражение);
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Расчет курса валюты ""%1"" по формуле ""%2"" не выполнен:'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()), Валюта, Формула);
			
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Валюты.Загрузка курсов валют'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка, Валюта.Метаданные(), Валюта, 
			ТекстОшибки + Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки + Символы.ПС + КраткоеПредставлениеОшибки(ИнформацияОбОшибке));
		Результат = Неопределено;
	КонецПопытки;
	
	Возврат Результат;
КонецФункции

#КонецОбласти

#КонецЕсли