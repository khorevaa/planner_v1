#Область УправлениеФормой
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НачалоРабочегоВремениРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НовоеЗначение = ЭтотОбъект.НачалоРабочегоВремени + Направление;
	Если НовоеЗначение > 23 Тогда
		НовоеЗначение = 23;
	ИначеЕсли НовоеЗначение < 0 Тогда
		НовоеЗначение = 0;
	КонецЕсли;
	ЭтотОбъект.НачалоРабочегоВремени = НовоеЗначение;
	
КонецПроцедуры

&НаКлиенте
Процедура ОкончаниеРабочегоВремениРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НовоеЗначение = ЭтотОбъект.ОкончаниеРабочегоВремени + Направление;
	Если НовоеЗначение > 24 Тогда
		НовоеЗначение = 24;
	ИначеЕсли НовоеЗначение < 1 Тогда
		НовоеЗначение = 1;
	КонецЕсли;
	ЭтотОбъект.ОкончаниеРабочегоВремени = НовоеЗначение;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Инициализация всех настроек планировщика
	ДанныеНастроек = Новый Соответствие;
	ДобавитьНастройкуПоДанным(ДанныеНастроек, Перечисления.ВидыНастроекПланировщика.СистемнаяУчетнаяЗапись);
	ДобавитьНастройкуПоДанным(ДанныеНастроек, Перечисления.ВидыНастроекПланировщика.ИспользоватьDHTMLX);
	ДобавитьНастройкуПоДанным(ДанныеНастроек, Перечисления.ВидыНастроекПланировщика.НачалоРабочегоВремени);
	ДобавитьНастройкуПоДанным(ДанныеНастроек, Перечисления.ВидыНастроекПланировщика.ОкончаниеРабочегоВремени);
	
	// Записываем в реквизит формы
	ЭтотОбъект.ТекущийПользователь		= Пользователи.ТекущийПользователь();
	ЭтотОбъект.ЭтоАдминистратор			= Пользователи.ЭтоПолноправныйПользователь(ЭтотОбъект.ТекущийПользователь, Истина);
	ЭтотОбъект.НастройкиПланировщика	= ОбщегоНазначения.ФиксированныеДанные(ДанныеНастроек);
	
	// Заполним данные настроек на форме
	ПрочитатьНастройкиНаСервере();
	
	// Если это не администратор тогда общие настройки не отображаем
	Если НЕ ЭтотОбъект.ЭтоАдминистратор Тогда
		Элементы.СтраницыНастройки.ОтображениеСтраниц	= ОтображениеСтраницФормы.Нет;
		Элементы.СтраницаОбщиеНастройки.Видимость		= Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	ПроверяемыеРеквизиты.Добавить("СистемнаяУчетнаяЗапись");
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если ЭтотОбъект.Модифицированность Тогда
		СтандартнаяОбработка	= Ложь;
		ОписаниеОповещения		= Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
		
		ПоказатьВопрос(ОписаниеОповещения,
			НСтр("ru='Настройки были изменены. Записать?'"),
			РежимДиалогаВопрос.ДаНетОтмена,,,,
			КодВозвратаДиалога.Отмена);
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	ПередЗакрытиемЗавершение(КодВозвратаДиалога.Да, Новый Структура);
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	ЗаписатьНастройкиНаСервере();
	ЭтотОбъект.Модифицированность = Ложь;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте 
Процедура ПередЗакрытиемЗавершение(Результат, ДопПараметры) Экспорт
	Если Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	ИначеЕсли Результат = КодВозвратаДиалога.Да Тогда
		Записать(Неопределено);
	КонецЕсли;
	
	ЭтотОбъект.Закрыть(Истина);
КонецПроцедуры

&НаСервереБезКонтекста 
Процедура ДобавитьНастройкуПоДанным(ДанныеНастройки, Настройка)
	
	НастройкиПланировщика = Перечисления.ВидыНастроекПланировщика.ПолучитьДанныеНастройки(Настройка);
	Если НастройкиПланировщика.Подсистема = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеНастройки.Вставить(ОбщегоНазначения.ИмяЗначенияПеречисления(Настройка), НастройкиПланировщика);
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНастройкиНаСервере()
	ЗаписатьНастройкиОбщиеНаСервере();
	ЗаписатьНастройкиИндивидуальныеНаСервере();
КонецПроцедуры

&НаСервере 
Процедура ПрочитатьНастройкиНаСервере()
	
	НастройкиПользователей	= Перечисления.ВидыНастроекПланировщика.ПолучитьНастройкиПользователей();
	НастройкиОбщие			= Новый Массив;
	
	Для Каждого КлючИЗначение Из ЭтотОбъект.НастройкиПланировщика Цикл
		Если НастройкиПользователей.Найти(КлючИЗначение.Значение.Настройка) = Неопределено Тогда
			НастройкиОбщие.Добавить(КлючИЗначение.Значение.Настройка);
		КонецЕсли;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ЭтоАдминистратор"		, ЭтотОбъект.ЭтоАдминистратор);
	Запрос.УстановитьПараметр("НастройкиОбщие"			, НастройкиОбщие);
	Запрос.УстановитьПараметр("НастройкиПользователей"	, НастройкиПользователей);
	Запрос.УстановитьПараметр("ТекущийПользователь"		, ЭтотОбъект.ТекущийПользователь);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиПланировщика.Настройка,
	|	НастройкиПланировщика.ЗначениеСтрока,
	|	НастройкиПланировщика.ЗначениеЧисло,
	|	НастройкиПланировщика.ЗначениеДата,
	|	НастройкиПланировщика.ЗначениеСсылка,
	|	НастройкиПланировщика.ЗначениеБулево,
	|	НастройкиПланировщика.ИмяРесурса
	|ИЗ
	|	РегистрСведений.НастройкиПланировщика КАК НастройкиПланировщика
	|ГДЕ
	|	&ЭтоАдминистратор
	|	И НастройкиПланировщика.Настройка В(&НастройкиОбщие)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НастройкиПланировщика.Настройка,
	|	НастройкиПланировщика.ЗначениеСтрока,
	|	НастройкиПланировщика.ЗначениеЧисло,
	|	НастройкиПланировщика.ЗначениеДата,
	|	НастройкиПланировщика.ЗначениеСсылка,
	|	НастройкиПланировщика.ЗначениеБулево,
	|	НастройкиПланировщика.ИмяРесурса
	|ИЗ
	|	РегистрСведений.НастройкиПланировщика КАК НастройкиПланировщика
	|ГДЕ
	|	НастройкиПланировщика.Настройка В(&НастройкиПользователей)
	|	И НастройкиПланировщика.Пользователь = &ТекущийПользователь";
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ИмяНастройки = ОбщегоНазначения.ИмяЗначенияПеречисления(Выборка.Настройка);
		ЭтотОбъект[ИмяНастройки] = Выборка[Выборка.ИмяРесурса];
	КонецЦикла;
	
	// Нормализуем значения настроек
	Если ЭтотОбъект.ОкончаниеРабочегоВремени = 0 Тогда
		ЭтотОбъект.ОкончаниеРабочегоВремени = 24;
	КонецЕсли;

	
КонецПроцедуры

&НаСервере 
Процедура ЗаписатьНастройкиОбщиеНаСервере()
	Если НЕ ЭтотОбъект.ЭтоАдминистратор Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиНабор = РегистрыСведений.НастройкиПланировщика.СоздатьНаборЗаписей();
	НастройкиНабор.Отбор.Пользователь.Установить(Справочники.Пользователи.ПустаяСсылка());
	НастройкиНабор.Прочитать();
	НастройкиНабор.Очистить();
	
	Для Каждого КлючИЗначение Из ЭтотОбъект.НастройкиПланировщика Цикл
		Если КлючИЗначение.Значение.Пользователь Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяНастройки		= КлючИЗначение.Ключ;
		ПараметрыНастройки	= КлючИЗначение.Значение;
		ЗначениеНастройки	= ЭтотОбъект[ИмяНастройки];
		
		МассивТиповНастройки = ПараметрыНастройки.Типы;
		Если ЗначениеЗаполнено(ЗначениеНастройки) И МассивТиповНастройки.Найти(ТипЗнч(ЗначениеНастройки)) = Неопределено Тогда
			Продолжить;			
		КонецЕсли;
		
		ЗаписьНастройки = НастройкиНабор.Добавить();
		ЗаписьНастройки.Настройка		= ПараметрыНастройки.Настройка;
		ЗаписьНастройки.Подсистема		= ПараметрыНастройки.Подсистема;
		ЗаписьНастройки.Пользователь	= Неопределено;
		ЗаписьНастройки[ПараметрыНастройки.ИмяРесурса] = ЗначениеНастройки;
		ЗаписьНастройки.ИмяРесурса		= ПараметрыНастройки.ИмяРесурса;
	КонецЦикла;
	
	НастройкиНабор.Записать(Истина);
КонецПроцедуры

&НаСервере 
Процедура ЗаписатьНастройкиИндивидуальныеНаСервере()
	Если НЕ ЗначениеЗаполнено(ЭтотОбъект.ТекущийПользователь) Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиНабор = РегистрыСведений.НастройкиПланировщика.СоздатьНаборЗаписей();
	НастройкиНабор.Отбор.Пользователь.Установить(ЭтотОбъект.ТекущийПользователь);
	НастройкиНабор.Прочитать();
	НастройкиНабор.Очистить();
	
	Для Каждого КлючИЗначение Из ЭтотОбъект.НастройкиПланировщика Цикл
		Если НЕ КлючИЗначение.Значение.Пользователь Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяНастройки		= КлючИЗначение.Ключ;
		ПараметрыНастройки	= КлючИЗначение.Значение;
		ЗначениеНастройки	= ЭтотОбъект[ИмяНастройки];
		
		МассивТиповНастройки = ПараметрыНастройки.Типы;
		Если ЗначениеЗаполнено(ЗначениеНастройки) И МассивТиповНастройки.Найти(ТипЗнч(ЗначениеНастройки)) = Неопределено Тогда
			Продолжить;			
		КонецЕсли;
		
		ЗаписьНастройки = НастройкиНабор.Добавить();
		ЗаписьНастройки.Настройка		= ПараметрыНастройки.Настройка;
		ЗаписьНастройки.Подсистема		= ПараметрыНастройки.Подсистема;
		ЗаписьНастройки.Пользователь	= ЭтотОбъект.ТекущийПользователь;
		ЗаписьНастройки[ПараметрыНастройки.ИмяРесурса] = ЗначениеНастройки;
		ЗаписьНастройки.ИмяРесурса		= ПараметрыНастройки.ИмяРесурса;
	КонецЦикла;
	
	НастройкиНабор.Записать(Истина);
КонецПроцедуры

#КонецОбласти

