#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Сообщает все требуемые сведения о процедуре загрузки данных из файла.
//
// Возвращаемое значение
//  Структура - содержит структуру со свойствами:
//     * Представление                           - Строка - Представление в списке вариантов загрузки.
//     * ИмяМакетаСШаблоном						 - Строка - Название макета со структурой данных(необязательный параметр, значение по умолчанию - "ЗагрузкаДанныхИзФайла").
//     * ОбязательныеКолонкиМакета                        - Массив - Содержит список обязательных полей для заполнения.
//     * ЗаголовокКолонкиСопоставления	  		 - Строка - Представление колонки сопоставления в шапке таблицы сопоставления данных(необязательный параметр, значение по умолчанию формируются - "Справочник: <синоним справочника>").
//     * ИмяОбъекта								 - Строка - Имя Объекта
//
Функция ПараметрыЗагрузкиИзФайла(МетаданныеСправочника = Неопределено) Экспорт
	
	ОбязательныеКолонкиМакета = Новый Массив;
	Для каждого Реквизит из МетаданныеСправочника.Реквизиты Цикл
		Если Реквизит.ПроверкаЗаполнения=ПроверкаЗаполнения.ВыдаватьОшибку тогда 
			ОбязательныеКолонкиМакета.Добавить(Реквизит.Имя);
		КонецЕсли;
	КонецЦикла;
		
	ПараметрыПоУмолчанию = Новый Структура;
	ПараметрыПоУмолчанию.Вставить("Заголовок", МетаданныеСправочника.Представление());
	ПараметрыПоУмолчанию.Вставить("ОбязательныеКолонки", ОбязательныеКолонкиМакета);
	ПараметрыПоУмолчанию.Вставить("ТипДанныхКолонки", Новый Соответствие);
	Возврат ПараметрыПоУмолчанию;
КонецФункции	

// Сообщает все требуемые сведения о процедуре загрузки данных из файла в Табличную часть
Функция ПараметрыЗагрузкиИзФайлаВТЧ(ИмяТабличнойЧасти) Экспорт
	
	ПараметрыПоУмолчанию= новый Структура;
	ПараметрыПоУмолчанию.Вставить("ОбязательныеКолонки",Новый Массив);
	ПараметрыПоУмолчанию.Вставить("ИмяМакетаСШаблоном","ЗагрузкаИзФайла");
	ПараметрыПоУмолчанию.Вставить("ИмяТабличнойЧасти", ИмяТабличнойЧасти);
	
	Возврат ПараметрыПоУмолчанию;
	
КонецФункции

// Сообщает все требуемые сведения о процедуре загрузки данных из файла для внешней обработки.
//
// Параметры: 
//    ИмяКоманды - Строка - Имя команды (Идентификатор)
//    СсылкаНаОбработку - Ссылка - Ссылка на обработку 
//    ИмяМакетаСШаблоном - Строка - Имя шаблона с макетом колонок для загрузки данных 
// Возвращаемое значение
//  Структура - содержит структуру со свойствами:
//     * Представление                           - Строка - Представление в списке вариантов загрузки.
//     * ИмяМакетаСШаблоном                      - Строка - Название макета со структурой данных(необязательный параметр, значение по умолчанию - "ЗагрузкаДанныхИзФайла").
//     * ОбязательныеКолонкиМакета               - Массив - Содержит список обязательных полей для заполнения.
//     * ЗаголовокКолонкиСопоставления            - Строка - Представление колонки сопоставления в шапке таблицы сопоставления данных(необязательный параметр, значение по умолчанию формируются - "Справочник: <синоним справочника>").
//     * ИмяОбъекта                               - Строка - Имя Объекта
//
Функция ПараметрыЗагрузкиИзФайлаВнешняяОбработка(ИмяКоманды, СсылкаНаОбработку, ИмяМакетаСШаблоном) Экспорт
	ОбязательныеКолонкиМакета = Новый Массив;
	
	Если НЕ ЗначениеЗаполнено(ИмяМакетаСШаблоном) Тогда 
		ИмяМакетаСШаблоном = "ЗагрузкаИзФайла";
	КонецЕсли;
	
	ПараметрыЗагрузки = Новый Структура;
	ПараметрыЗагрузки.Вставить("ИмяМакетаСШаблоном", ИмяМакетаСШаблоном);
	ПараметрыЗагрузки.Вставить("ОбязательныеКолонкиМакета", ОбязательныеКолонкиМакета);
	ПараметрыЗагрузки.Вставить("СоответствиеТипаДанныхКолонок", Новый Соответствие);
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки") Тогда
		МодульДополнительныеОтчетыИОбработки = ОбщегоНазначения.ОбщийМодуль("ДополнительныеОтчетыИОбработки");
		ВнешнийОбъект = МодульДополнительныеОтчетыИОбработки.ПолучитьОбъектВнешнейОбработки(СсылкаНаОбработку);
	КонецЕсли;
	
	Попытка
		ВнешнийОбъект.ОпределитьПараметрыЗагрузкиДанныхИзФайла(ИмяКоманды, ПараметрыЗагрузки);
	Исключение
		// Если функция определена во внешней обработке, то он переопределяет настройки по умолчанию.
	КонецПопытки;
	
	ПараметрыЗагрузки.Вставить("Макет", ВнешнийОбъект.МакетСШаблоном(ПараметрыЗагрузки.ИмяМакетаСШаблоном));
	
	Возврат ПараметрыЗагрузки;
КонецФункции

// Создает таблицу значений со списком команд во внешней обработке для загрузки из файла  
//
Процедура ЗадатьКомандыЗагрузки(ПараметрыРегистрации) Экспорт
	
	ТаблицаКоманд = Новый ТаблицаЗначений;
	ТаблицаКоманд.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	ТаблицаКоманд.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
	ТаблицаКоманд.Колонки.Добавить("МакетСШаблоном", Новый ОписаниеТипов("Строка"));
	ТаблицаКоманд.Колонки.Добавить("ПолноеИмяОбъектаМетаданных", Новый ОписаниеТипов("Строка"));
	
	ПараметрыРегистрации.Вставить("КомандыЗагрузки", ТаблицаКоманд);
	
КонецПроцедуры

#Область СлужебныеФункции

// Формирует информацию по колонкам на основание шаблона и описания типов 
//
Процедура ИнициализироватьРежимПоискСсылок(ШаблонСДанными, ИнформацияПоКолонкам, ОписаниеТипов) Экспорт
	СоответствиеКолонок = Новый Соответствие;
	ЗаголовокКолонки = "";
	Разделитель = "";
	
	Для каждого Тип Из ОписаниеТипов.Типы() Цикл 
		ОбъектМетаданных = Метаданные.НайтиПоТипу(Тип);
		СтруктураОбъекта = РазложитьПолноеИмяОбъекта(ОбъектМетаданных.ПолноеИмя());
		
		Для каждого Колонка Из ОбъектМетаданных.ВводПоСтроке Цикл 
			Если СоответствиеКолонок.Получить(Колонка.Имя) = Неопределено Тогда 
				
				ЗаголовокКолонки = ЗаголовокКолонки + Разделитель +Колонка.Имя;
				Разделитель = ", ";
				СоответствиеКолонок.Вставить(Колонка.Имя, Колонка.Имя);
			КонецЕсли;
		КонецЦикла;
		Если СтруктураОбъекта.ТипОбъекта = "Документ" Тогда 
			ЗаголовокКолонки = ЗаголовокКолонки + Разделитель + "Представление";
		КонецЕсли;
		
		ЗаголовокКолонки = НСтр("ru = 'Введенные данные'");
		
	КонецЦикла;
	
	ДобавитьИнформациюПоКолонке(ИнформацияПоКолонкам, "Ссылки", ЗаголовокКолонки, Новый ОписаниеТипов("Строка"), Ложь, 1);
	
	Шапка = ШапкаБланкаДляЗаполненияПоИнформацииПоКолонкам(ИнформацияПоКолонкам);
	ШаблонСДанными.Очистить();
	ШаблонСДанными.Вывести(Шапка);
	
	
КонецПроцедуры

// Выполняет автоматическое сопоставление данных для режима вставки данных из буфера обмена
//
Процедура СопоставитьЗначениеКолонкиАвто(ТаблицаСопоставления, ИмяКолонки) Экспорт
	
	Типы = ТаблицаСопоставления.Колонки.ОбъектСопоставления.ТипЗначения.Типы();
	ТекстЗапроса = "";
	Для каждого Тип Из Типы Цикл
		ОбъектМетаданных = Метаданные.НайтиПоТипу(Тип);
		СтруктураОбъекта = РазложитьПолноеИмяОбъекта(ОбъектМетаданных.ПолноеИмя());
		
		МассивКолонок = Новый Массив;
		Для каждого Поле Из ОбъектМетаданных.ВводПоСтроке Цикл
			МассивКолонок.Добавить(Поле.Имя);
		КонецЦикла;
		Если СтруктураОбъекта.ТипОбъекта = "Документ" Тогда
			МассивКолонок.Добавить("Ссылка");
		КонецЕсли;
		
		ТекстЗапроса = СтрокаЗапроса(ТекстЗапроса, СтруктураОбъекта.ТипОбъекта,
		СтруктураОбъекта.НазваниеОбъекта, МассивКолонок);
	КонецЦикла;
	
	Для каждого Строка из ТаблицаСопоставления Цикл 
		Если НЕ ЗначениеЗаполнено(Строка[ИмяКолонки]) Тогда 
			Продолжить;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТекстЗапроса) Тогда
			Значение = ДокументПоПредставлению(Строка[ИмяКолонки], Типы);
			Если Значение = Неопределено Тогда
				Значение = Строка[ИмяКолонки];
			КонецЕсли;
			МассивСсылок = НайтиСсылкиПоПараметрамОтбора(ТекстЗапроса, Значение);
			Если МассивСсылок.Количество() = 1 Тогда
				Строка.ОбъектСопоставления = МассивСсылок[0];
				Строка.РезультатСопоставленияСтроки = "СтрокаСопоставлена";
			ИначеЕсли МассивСсылок.Количество() > 1 Тогда
				СписокНеоднозначностей = Новый СписокЗначений;
				Строка.СписокНеоднозначностей.ЗагрузитьЗначения(МассивСсылок);
				Строка.РезультатСопоставленияСтроки = "Неоднозначность";
			Иначе
				Строка.РезультатСопоставленияСтроки = "НеСопоставлен";
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

функция ДокументПоПредставлению(Представление, Типы)
	
	Для каждого Тип Из Типы Цикл 
		ОбъектМетаданных = Метаданные.НайтиПоТипу(Тип);
		СтруктураИмениОбъекта = РазложитьПолноеИмяОбъекта(ОбъектМетаданных.ПолноеИмя());
		если СтруктураИмениОбъекта.ТипОбъекта <> "Документ" Тогда
			Продолжить;
		КонецЕсли;
		
		СтандартныеСвойства = Новый Структура("ПредставлениеОбъекта, РасширенноеПредставлениеОбъекта, ПредставлениеСписка, РасширенноеПредставлениеСписка");
		ЗаполнитьЗначенияСвойств(СтандартныеСвойства, ОбъектМетаданных);
		
		Если ЗначениеЗаполнено(СтандартныеСвойства.ПредставлениеОбъекта) Тогда
			ПредставлениеЭлемента = СтандартныеСвойства.ПредставлениеОбъекта;
		ИначеЕсли ЗначениеЗаполнено(СтандартныеСвойства.РасширенноеПредставлениеОбъекта) Тогда
			ПредставлениеЭлемента = СтандартныеСвойства.РасширенноеПредставлениеОбъекта;
		Иначе
			ПредставлениеЭлемента = ОбъектМетаданных.Представление();
		КонецЕсли;
		
		Если Найти(Представление, ПредставлениеЭлемента) > 0 Тогда
			ПредставлениеНомерИДата = СокрЛП(Сред(Представление, СтрДлина(ПредставлениеЭлемента) + 1));
			ПозицияОкончанияНомера = Найти(ПредставлениеНомерИДата, " ");
			Номер = Лев(ПредставлениеНомерИДата, ПозицияОкончанияНомера - 1);
			ПозицияОт = Найти(НРег(ПредставлениеНомерИДата), "от");
			ПредставлениеДата = СокрЛ(Сред(ПредставлениеНомерИДата, ПозицияОт + 2));
			ПозицияОкончаниеДаты = Найти(ПредставлениеДата, " ");
			ДатаОкругленнаяДоДня = Лев(ПредставлениеДата, ПозицияОкончаниеДаты - 1) + " 00:00:00";
			НомерДокумент = Номер;
			ДатаДокумента = ПреобразоватьВДату(ДатаОкругленнаяДоДня);
		КонецЕсли;
		Документ = Документы[ОбъектМетаданных.Имя].НайтиПоНомеру(НомерДокумент, ДатаДокумента);
		
		Если НЕ (Документ = Неопределено ИЛИ Документ = Документы[ОбъектМетаданных.Имя].ПустаяСсылка()) Тогда
			Возврат Документ;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

Функция ПреобразоватьВДату(ДатаСтрока)

	Попытка
		Возврат Дата(ДатаСтрока);
	Исключение
		Возврат Неопределено;
	КонецПопытки;
	
КонецФункции 

Функция СтрокаЗапроса(ТекстЗапроса, ТипОбъекта, ИмяОбъекта, МассивКолонок)
	
	Если МассивКолонок.Количество() > 0 Тогда
		ТекстГде = "";
		РазделительГде = "";
		Для Каждого Поле из МассивКолонок Цикл 
			ТекстГде = ТекстГде + РазделительГде + ИмяОбъекта + "." + Поле + " = &ПараметрПоиска";
			РазделительГде = " ИЛИ ";
		КонецЦикла;
		
		ТекстШаблон = "ВЫБРАТЬ %1.Ссылка КАК СсылкаНаОбъект ИЗ %2.%1 КАК %1 ГДЕ " + ТекстГде;
		Если ЗначениеЗаполнено(ТекстЗапроса) Тогда 
			ТекстОбъеденитьВсе = Символы.ПС + "ОБЪЕДИНИТЬ ВСЕ" + Символы.ПС;
		Иначе
			ТекстОбъеденитьВсе = "";
		КонецЕсли;
		ТекстЗапроса = ТекстЗапроса + ТекстОбъеденитьВсе + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстШаблон, ИмяОбъекта, ТипОбъекта);
	КонецЕсли;
	Возврат ТекстЗапроса;
	
КонецФункции

Функция НайтиСсылкиПоПараметрамОтбора(ТекстЗапроса, Значение)
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ПараметрПоиска", Значение);
	
	ТаблицаРезультатов = Запрос.Выполнить().Выгрузить();
	МассивРезультат = ТаблицаРезультатов.ВыгрузитьКолонку("СсылкаНаОбъект");
	Возврат МассивРезультат;
КонецФункции

// Создает список справочников доступных для загрузки
//
Процедура СоздатьСписокСправочниковДляЗагрузки(СписокСправочниковДляЗагрузки) Экспорт
	
	ТипСтрока = Новый ОписаниеТипов("Строка");
	ИнформацияОСправочниках = Новый ТаблицаЗначений;
	ИнформацияОСправочниках.Колонки.Добавить("ПолноеИмяОбъектаМетаданных", ТипСтрока);
	ИнформацияОСправочниках.Колонки.Добавить("Представление", ТипСтрока);
	ИнформацияОСправочниках.Колонки.Добавить("Параметры");
	
	СправочникиИсключения = СписокСправочниковИсключений();
	
	Для каждого ОбъектМетаданныхДляВывода Из Метаданные.Справочники Цикл
		Если СправочникиИсключения.Найти(ОбъектМетаданныхДляВывода.Имя) = Неопределено Тогда
			ИнформацияОТипеЗагрузки = Новый Структура();
			ИнформацияОТипеЗагрузки.Вставить("Тип", "УниверсальнаяЗагрузка");
			ИнформацияОТипеЗагрузки.Вставить("ПолноеИмяОбъектаМетаданных", ОбъектМетаданныхДляВывода.ПолноеИмя());
			
			Строка = ИнформацияОСправочниках.Добавить();
			Строка.ПолноеИмяОбъектаМетаданных = ОбъектМетаданныхДляВывода.ПолноеИмя();
			Строка.Представление = ОбъектМетаданныхДляВывода.Представление();
			Строка.Параметры = ИнформацияОТипеЗагрузки;
		КонецЕсли;
	КонецЦикла;
	
	Для каждого Справочник из Метаданные.Справочники Цикл
		Менеджер = Справочники[Справочник.Имя];
		
		Если Справочник.Макеты.Найти("ЗагрузкаИзФайла") <> Неопределено тогда
			
			ПараметрыЗагрузки = ПараметрыЗагрузкиИзФайла(Справочник);
			Попытка
				Менеджер.ОпределитьПараметрыЗагрузкиДанныхИзФайла(ПараметрыЗагрузки);
			Исключение
			КонецПопытки;
			
			СправочникПредставление = ПараметрыЗагрузки.Заголовок;
			ИнформацияОТипеЗагрузки = Новый Структура();
			ИнформацияОТипеЗагрузки.Вставить("Тип", "ПрикладнаяЗагрузка");
			ИнформацияОТипеЗагрузки.Вставить("ПолноеИмяОбъектаМетаданных", Справочник.ПолноеИмя());
			
			Строка = ИнформацияОСправочниках.Найти(Справочник.ПолноеИмя(), "ПолноеИмяОбъектаМетаданных");
			Если Строка = Неопределено Тогда 
				Строка = ИнформацияОСправочниках.Добавить();
				Строка.ПолноеИмяОбъектаМетаданных = ОбъектМетаданныхДляВывода.ПолноеИмя();
				Строка.Представление = СправочникПредставление;
				Строка.Параметры = ИнформацияОТипеЗагрузки;
			Иначе
				Строка.Параметры = ИнформацияОТипеЗагрузки;
				Строка.Представление = СправочникПредставление;
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки") Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ДополнительныеОтчетыИОбработкиКоманды.Ссылка,
		|	ДополнительныеОтчетыИОбработкиКоманды.Идентификатор,
		|	ДополнительныеОтчетыИОбработкиКоманды.Представление,
		|	ДополнительныеОтчетыИОбработкиКоманды.Модификатор
		|ИЗ
		|	Справочник.ДополнительныеОтчетыИОбработки.Команды КАК ДополнительныеОтчетыИОбработкиКоманды
		|ГДЕ
		|	ДополнительныеОтчетыИОбработкиКоманды.ВариантЗапуска = &ВариантЗапуска
		|	И ДополнительныеОтчетыИОбработкиКоманды.Ссылка.Вид = &Вид
		|	И Не ДополнительныеОтчетыИОбработкиКоманды.Ссылка.ПометкаУдаления
		|	И ДополнительныеОтчетыИОбработкиКоманды.Ссылка.Публикация = &Публикация";
		Запрос.УстановитьПараметр("ВариантЗапуска", Перечисления.СпособыВызоваДополнительныхОбработок.ЗагрузкаДанныхИзФайла);
		Запрос.УстановитьПараметр("Вид", Перечисления.ВидыДополнительныхОтчетовИОбработок.ДополнительнаяОбработка);
		Запрос.УстановитьПараметр("Публикация", Перечисления.ВариантыПубликацииДополнительныхОтчетовИОбработок.Используется);
		ТаблицаКоманд = Запрос.Выполнить().Выгрузить();
		
		Для Каждого СтрокаТаблицы Из ТаблицаКоманд Цикл
			ИнформацияОТипеЗагрузки = Новый Структура;
			ИнформацияОТипеЗагрузки.Вставить("Тип", "ВнешняяЗагрузка");
			ИнформацияОТипеЗагрузки.Вставить("ПолноеИмяОбъектаМетаданных", СтрокаТаблицы.Идентификатор);
			ИнформацияОТипеЗагрузки.Вставить("Ссылка", СтрокаТаблицы.Ссылка);
			ИнформацияОТипеЗагрузки.Вставить("МакетСШаблоном", СтрокаТаблицы.Модификатор);
			
			Строка = ИнформацияОСправочниках.Добавить();
			Строка.ПолноеИмяОбъектаМетаданных = ОбъектМетаданныхДляВывода.Имя;
			Строка.Параметры = ИнформацияОТипеЗагрузки;
			Строка.Представление = СтрокаТаблицы.Представление;
		КонецЦикла;
	КонецЕсли;
	
	СписокСправочниковДляЗагрузки.Очистить();
	Для каждого строка Из ИнформацияОСправочниках Цикл
		СписокСправочниковДляЗагрузки.Добавить(Строка.Параметры, Строка.Представление);
	КонецЦикла;
		
	СписокСправочниковДляЗагрузки.СортироватьПоПредставлению();

	
КонецПроцедуры 

Функция СписокСправочниковИсключений()
	
	СправочникиИсключения = Новый Массив;
	
	Для каждого Справочник из Метаданные.Справочники Цикл
		
		Если СправочникСодержитРеквизитИсключение(Справочник) Тогда
			Попытка
				Менеджер = Справочники[Справочник.Имя];
				Если Менеджер.ИспользоватьЗагрузкуДанныхИзФайла() Тогда
					Продолжить;
				КонецЕсли;
			Исключение
				// Функция разрешения загрузки не определена в модуле менеджера справочника.
			КонецПопытки;
			СправочникиИсключения.Добавить(Справочник.Имя);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СправочникиИсключения;
	
КонецФункции

Функция СправочникСодержитРеквизитИсключение(Справочник)
	
	Для каждого Реквизит Из Справочник.ТабличныеЧасти Цикл
		Если Реквизит.Имя <> "КонтактнаяИнформация" И
			Реквизит.Имя <> "ДополнительныеРеквизиты" И
			Реквизит.Имя <> "СертификатыШифрования" Тогда
				Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Для каждого Реквизит Из Справочник.Реквизиты Цикл 
		Для каждого ТипРеквизита Из Реквизит.Тип.Типы() Цикл
			Если ТипРеквизита = Тип("ХранилищеЗначения") Тогда
				Возврат Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Если ТРег(Лев(Справочник.Имя, 7)) = "Удалить" Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Заполняет таблицу значений Сопоставленные данные по данным из макета
//
Процедура ЗаполнитьТаблицуСопоставленияДаннымиИзШаблонаФон(ПараметрыВыгрузки, АдресХранилища) Экспорт
	
	ШаблонСДанными = ПараметрыВыгрузки.ШаблонСДанными;
	ТаблицаСопоставления = ПараметрыВыгрузки.ТаблицаСопоставления;
	ИнформацияПоКолонкам = ПараметрыВыгрузки.ИнформацияПоКолонкам;
	
	ТаблицаСопоставления.Очистить();
	ЗаполнитьТаблицуСопоставленияЗагружаемымиДанными(ШаблонСДанными, ИнформацияПоКолонкам, ТаблицаСопоставления, Истина);
	
	ПоместитьВоВременноеХранилище(ТаблицаСопоставления, АдресХранилища);
	
КонецПроцедуры

// Заполняет таблицу данными из загруженного шаблона 
//
Процедура ЗаполнитьТаблицуСопоставленияДаннымиИзШаблона(ШаблонСДанными, ТаблицаСопоставления, ИнформацияПоКолонкам) Экспорт
	
	ОпределитьПозицииКолонокВМакете(ШаблонСДанными, ИнформацияПоКолонкам);
	ТаблицаСопоставления.Очистить();
	ЗаполнитьТаблицуСопоставленияЗагружаемымиДанными(ШаблонСДанными, ИнформацияПоКолонкам, ТаблицаСопоставления);
	
КонецПроцедуры

Процедура ЗаполнитьТаблицуСопоставленияЗагружаемымиДанными(ШаблонСДанными, ТаблицаИнформацияПоКолонкам, ТаблицаСопоставления, ФоновоеЗадание = Ложь)
	
	Для НомерСтроки = 2 По ШаблонСДанными.ВысотаТаблицы Цикл 
		НоваяСтрока = ТаблицаСопоставления.Добавить();
		НоваяСтрока.Идентификатор = НомерСтроки - 1;
		НоваяСтрока.РезультатСопоставленияСтроки = "НеСопоставлен";
		Для НомерКолонки = 1 По ШаблонСДанными.ШиринаТаблицы Цикл 
			Ячейка = ШаблонСДанными.ПолучитьОбласть(НомерСтроки, НомерКолонки, НомерСтроки, НомерКолонки).ТекущаяОбласть;
			
			Колонка = НайтиИнформациюОКолонке(ТаблицаИнформацияПоКолонкам, "Позиция", НомерКолонки);
			
			Если Колонка <> Неопределено Тогда 
					ИмяКолонки = Колонка.ИмяКолонки;
				
				ТипДанных = ТипЗнч(НоваяСтрока[ИмяКолонки]);
				
				Если ТипДанных <> Тип("Строка") И ТипДанных <> Тип("Булево") И ТипДанных <> Тип("Число") И ТипДанных <> Тип("Дата")  И ТипДанных <> Тип("УникальныйИдентификатор") Тогда 
					ДанныеЯчейки = ЗначениеЯчейки(Колонка, Ячейка.Текст);
				Иначе
					ДанныеЯчейки = Ячейка.Текст;
				КонецЕсли;
				НоваяСтрока[ИмяКолонки] = ДанныеЯчейки;
			КонецЕсли;
		КонецЦикла;
		
		Если ФоновоеЗадание Тогда
			Процент = Окр(НомерСтроки *100 / ШаблонСДанными.ВысотаТаблицы);
			МодульДлительныеОперации = ОбщегоНазначения.ОбщийМодуль("ДлительныеОперации");
			МодульДлительныеОперации.СообщитьПрогресс(Процент);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция НайтиИнформациюОКолонке(ТаблицаИнформацияПоКолонкам, ИмяКолонки, Значение)
	
	
	Отбор = Новый Структура(ИмяКолонки, Значение);
	НайденныеКолонки = ТаблицаИнформацияПоКолонкам.НайтиСтроки(Отбор);
	Колонка = Неопределено;
	Если НайденныеКолонки.Количество() > 0 Тогда 
		Колонка = НайденныеКолонки[0];
	КонецЕсли;
	
	Возврат Колонка;
КонецФункции

Функция ЗначениеЯчейки(Колонка, ЗначениеЯчейки)
	
	ДанныеЯчейки = "";
	Для каждого ТипДанных Из Колонка.ТипКолонки.Типы() Цикл 
		Объект = Метаданные.НайтиПоТипу(ТипДанных);
		ОписаниеОбъекта = РазложитьПолноеИмяОбъекта(Объект.ПолноеИмя());
		Если ОписаниеОбъекта.ТипОбъекта = "Справочник" Тогда
			Если НЕ Объект.Автонумерация И Объект.ДлинаКода > 0 Тогда 
				ДанныеЯчейки = Справочники[ОписаниеОбъекта.НазваниеОбъекта].НайтиПоКоду(ЗначениеЯчейки, Истина);
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(ДанныеЯчейки) Тогда 
				ДанныеЯчейки = Справочники[ОписаниеОбъекта.НазваниеОбъекта].НайтиПоНаименованию(ЗначениеЯчейки, Истина);
			КонецЕсли;
		ИначеЕсли ОписаниеОбъекта.ТипОбъекта = "Перечисление" Тогда 
			Для каждого ЗначениеПеречисления Из Перечисления[ОписаниеОбъекта.НазваниеОбъекта] Цикл 
				Если Строка(ЗначениеПеречисления) = СокрЛП(ЗначениеЯчейки) Тогда 
					ДанныеЯчейки = ЗначениеПеречисления; 
				КонецЕсли;
			КонецЦикла;
		ИначеЕсли ОписаниеОбъекта.ТипОбъекта = "ПланСчетов" Тогда
			ДанныеЯчейки = ПланыСчетов[ОписаниеОбъекта.НазваниеОбъекта].НайтиПоКоду(ЗначениеЯчейки);
			Если ДанныеЯчейки.Пустая() Тогда 
				ДанныеЯчейки = ПланыСчетов[ОписаниеОбъекта.НазваниеОбъекта].НайтиПоНаименованию(ЗначениеЯчейки, Истина);
			КонецЕсли;
		ИначеЕсли ОписаниеОбъекта.ТипОбъекта = "ПланВидовХарактеристик" Тогда
			Если НЕ Объект.Автонумерация И Объект.ДлинаКода > 0 Тогда 
				ДанныеЯчейки = ПланыВидовХарактеристик[ОписаниеОбъекта.НазваниеОбъекта].НайтиПоКоду(ЗначениеЯчейки, Истина);
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(ДанныеЯчейки) Тогда 
				ДанныеЯчейки = ПланыВидовХарактеристик[ОписаниеОбъекта.НазваниеОбъекта].НайтиПоНаименованию(ЗначениеЯчейки, Истина);
			КонецЕсли;
		Иначе
			ДанныеЯчейки =  Строка(ЗначениеЯчейки);
		КонецЕсли;
		Если ЗначениеЗаполнено(ДанныеЯчейки) Тогда 
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ДанныеЯчейки;
	
КонецФункции

Процедура ОпределитьПозицииКолонокВМакете(ШаблонСДанными, ИнформацияПоКолонкам)
	
	ОбластьЗаголовка = ОбластьЗаголовкаШаблонаТаблицы(ШаблонСДанными);
	
	СоответствиеКолонок = Новый Соответствие;
	Для НомерКолонки = 1 По ОбластьЗаголовка.ШиринаТаблицы Цикл 
		Ячейка=ШаблонСДанными.ПолучитьОбласть(1, НомерКолонки, 1, НомерКолонки).ТекущаяОбласть;
		ИмяКолонкиВМакете = Ячейка.Текст;
		СоответствиеКолонок.Вставить(ИмяКолонкиВМакете, НомерКолонки);
	КонецЦикла;
	
	Для каждого Колонка Из ИнформацияПоКолонкам Цикл 
		Позиция = СоответствиеКолонок.Получить(Колонка.ПредставлениеКолонки);
		Если Позиция <> Неопределено Тогда 
			Колонка.Позиция = Позиция;
		Иначе
			Колонка.Позиция = -1;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ОбластьЗаголовкаШаблонаТаблицы(Шаблон)
	МетаданныеОбластьЗаголовокТаблицы = Шаблон.Области.Найти("Шапка");
	
	Если МетаданныеОбластьЗаголовокТаблицы = Неопределено тогда 
		ОбластьЗаголовокТаблицы = Шаблон.ПолучитьОбласть("R1");
	Иначе 
		ОбластьЗаголовокТаблицы = Шаблон.ПолучитьОбласть("Шапка"); 
	КонецЕсли;
	
	Возврат ОбластьЗаголовокТаблицы;
	
КонецФункции

// Формирует макет табличного документа на основание реквизитов справочника
//
Процедура ИнформациюПоКолонкамИзРеквизитовСправочника(ИмяОбъектаСопоставления, ИнформацияПоКолонкам) Экспорт
	
	ИнформацияПоКолонкам.Очистить();
	
	МетаданныеСправочника= Метаданные.НайтиПоПолномуИмени(ИмяОбъектаСопоставления);
	ОписаниеТиповСтрока = Новый ОписаниеТипов("Строка");
	
	ТекстПримечания = "";
	
	Позиция = 1;
	Если НЕ МетаданныеСправочника.Автонумерация И МетаданныеСправочника.ДлинаКода > 0  Тогда
		СоздатьКолонкуСтандартныеРеквизиты(ИнформацияПоКолонкам, МетаданныеСправочника, "Код", Позиция);
		Позиция = Позиция + 1;
	КонецЕсли;
	
	Если МетаданныеСправочника.ДлинаНаименования > 0  Тогда
		СоздатьКолонкуСтандартныеРеквизиты(ИнформацияПоКолонкам, МетаданныеСправочника, "Наименование", Позиция);
		Позиция = Позиция + 1;
	КонецЕсли;
	
	Если МетаданныеСправочника.Иерархический Тогда
		 СоздатьКолонкуСтандартныеРеквизиты(ИнформацияПоКолонкам, МетаданныеСправочника, "Родитель", Позиция);
		 Позиция = Позиция + 1;
	КонецЕсли;
	 
	Если МетаданныеСправочника.Владельцы.Количество() > 0 Тогда
		 СоздатьКолонкуСтандартныеРеквизиты(ИнформацияПоКолонкам, МетаданныеСправочника, "Владелец", Позиция);
		 Позиция = Позиция + 1;
	КонецЕсли;
	
	Для каждого Реквизит Из МетаданныеСправочника.Реквизиты Цикл
		
		Если Реквизит.Тип.СодержитТип(Тип("ХранилищеЗначения")) тогда
			Продолжить;
		КонецЕсли;
		
		ОписаниеТипаКолонки = "";
		
		Если Реквизит.Тип.СодержитТип(Тип("Булево")) Тогда 
			ОписаниеТипаКолонки = НСтр("ru = 'Флаг, Да или 1 / Нет или 0'");
		ИначеЕсли Реквизит.Тип.СодержитТип(Тип("Число")) Тогда 
			ОписаниеТипаКолонки =  СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Число, Длина: %1, Точность: %2'"),
			Строка(Реквизит.Тип.КвалификаторыЧисла.Разрядность),
			Строка(Реквизит.Тип.КвалификаторыЧисла.РазрядностьДробнойЧасти));
		ИначеЕсли Реквизит.Тип.СодержитТип(Тип("Строка")) Тогда 
			Если Реквизит.Тип.КвалификаторыСтроки.Длина > 0 Тогда 
				ДлинаСтроки = Строка(Реквизит.Тип.КвалификаторыСтроки.Длина);
				ОписаниеТипаКолонки =  СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Строка, макс. количество символов: %1'"), ДлинаСтроки);
			Иначе
				ОписаниеТипаКолонки = НСтр("ru = 'Строка неограниченой длины'");
			КонецЕсли;
		ИначеЕсли Реквизит.Тип.СодержитТип(Тип("Дата")) Тогда 
			ОписаниеТипаКолонки =  СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1'"),Строка(Реквизит.Тип.КвалификаторыДаты.ЧастиДаты));
		ИначеЕсли Реквизит.Тип.СодержитТип(Тип("УникальныйИдентификатор")) Тогда 
			ОписаниеТипаКолонки = НСтр("ru = 'УникальныйИдентификатор'");
		Иначе
			//ПолучитьДанныеПоКолонкамСсылочногоТипа(Реквизит.Тип, ОписаниеТипаКолонки);
		КонецЕсли;
		
		ШиринаКолонки = ШиринаКолонкиПоТипу(Реквизит.Тип);
		Подсказка = ?(ЗначениеЗаполнено(Реквизит.Подсказка), Реквизит.Подсказка, Реквизит.Представление()) +  Символы.ПС + ОписаниеТипаКолонки;
		ОбязательноеПоле =  ?(Реквизит.ПроверкаЗаполнения = ПроверкаЗаполнения.ВыдаватьОшибку, Истина, Ложь);
		
		СтрокаИнфоПроКолонки = ИнформацияПоКолонкам.Добавить();
		СтрокаИнфоПроКолонки.ИмяКолонки = Реквизит.Имя;
		СтрокаИнфоПроКолонки.ПредставлениеКолонки = Реквизит.Представление();
		СтрокаИнфоПроКолонки.ТипКолонки = Реквизит.Тип;
		СтрокаИнфоПроКолонки.ОбязательнаДляЗаполнения = ОбязательноеПоле;
		СтрокаИнфоПроКолонки.Позиция = Позиция;
		СтрокаИнфоПроКолонки.Видимость = Истина;
		СтрокаИнфоПроКолонки.Примечание = Подсказка;
		СтрокаИнфоПроКолонки.Ширина = ШиринаКолонки;

		Позиция = Позиция + 1;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ШиринаКолонкиПоТипу(Тип) 
	
	ШиринаКолонки = 20;
	Если Тип.СодержитТип(Тип("Булево")) Тогда 
		ШиринаКолонки = 3;
	ИначеЕсли Тип.СодержитТип(Тип("Число")) Тогда 
		ШиринаКолонки = Тип.КвалификаторыЧисла.Разрядность + 1;
	ИначеЕсли Тип.СодержитТип(Тип("Строка")) Тогда 
		Если Тип.КвалификаторыСтроки.Длина > 0 Тогда 
			ШиринаКолонки = ?(Тип.КвалификаторыСтроки.Длина > 20, 20, Тип.КвалификаторыСтроки.Длина);
		Иначе
			ШиринаКолонки = 20;
		КонецЕсли;
	ИначеЕсли Тип.СодержитТип(Тип("Дата")) Тогда 
		ШиринаКолонки = 12;
	ИначеЕсли Тип.СодержитТип(Тип("УникальныйИдентификатор")) Тогда 
		ШиринаКолонки = 20;
	Иначе
		Для каждого ТипОбъекта Из  Тип.Типы() Цикл
			МетаданныеОбъекта = Метаданные.НайтиПоТипу(ТипОбъекта);
			СтруктураОбъекта = РазложитьПолноеИмяОбъекта(МетаданныеОбъекта.ПолноеИмя());
			Если СтруктураОбъекта.ТипОбъекта = "Справочник" Тогда 
				Если НЕ МетаданныеОбъекта.Автонумерация И МетаданныеОбъекта.ДлинаКода > 0  Тогда
					ШиринаКолонки = МетаданныеОбъекта.ДлинаКода + 1;
				КонецЕсли;
				Если МетаданныеОбъекта.ДлинаНаименования > 0  Тогда
					Если МетаданныеОбъекта.ДлинаНаименования > ШиринаКолонки Тогда
						ШиринаКолонки = ?(МетаданныеОбъекта.ДлинаНаименования > 30, 30, МетаданныеОбъекта.ДлинаНаименования + 1);
					КонецЕсли;
			КонецЕсли;
		ИначеЕсли СтруктураОбъекта.ТипОбъекта = "Перечисление" Тогда
				ДлинаПредставления =  СтрДлина(МетаданныеОбъекта.Представление());
				ШиринаКолонки = ?( ДлинаПредставления > 30, 30, ДлинаПредставления + 1);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат ШиринаКолонки;
	
КонецФункции

Процедура СоздатьКолонкуСтандартныеРеквизиты(ИнформацияПоКолонкам, МетаданныеСправочника, ИмяКолонки, Позиция)
	
	Реквизит = МетаданныеСправочника.СтандартныеРеквизиты[ИмяКолонки];
	Представление = МетаданныеСправочника.СтандартныеРеквизиты[ИмяКолонки].Представление();
	ТипДанных = МетаданныеСправочника.СтандартныеРеквизиты[ИмяКолонки].Тип.Типы()[0];
	ОписаниеТипа = МетаданныеСправочника.СтандартныеРеквизиты[ИмяКолонки].Тип;
	
	ШиринаКолонки = 11;
	
	Если ТипДанных = Тип("Строка") Тогда 
		ПредставлениеТипа = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Строка (не более %1 символов)"), ОписаниеТипа.КвалификаторыСтроки.Длина);
		ШиринаКолонки = ?(ОписаниеТипа.КвалификаторыСтроки.Длина < 30, ОписаниеТипа.КвалификаторыСтроки.Длина + 1, 30);
	ИначеЕсли ТипДанных = Тип("Число") Тогда	
		ПредставлениеТипа = НСтр("ru = 'Число'");
	Иначе
		Если МетаданныеСправочника.СтандартныеРеквизиты[ИмяКолонки].Тип.Типы().Количество() = 1 Тогда 
			ПредставлениеТипа = Строка(ТипДанных); 
		Иначе
			ПредставлениеТипа = "";
			Разделитель = "";
			Для каждого ТипДанные Из МетаданныеСправочника.СтандартныеРеквизиты[ИмяКолонки].Тип.Типы() Цикл 
				ПредставлениеТипа = ПредставлениеТипа  + Разделитель + Строка(ТипДанные);
				Разделитель = " или ";
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	ТекстПримечания = Реквизит.Подсказка + Символы.ПС + ПредставлениеТипа;
	
	ОбязательнаДляЗаполнения = ?(Реквизит.ПроверкаЗаполнения = ПроверкаЗаполнения.ВыдаватьОшибку, Истина, Ложь);
	СтрокаИнфоПроКолонки = ИнформацияПоКолонкам.Добавить();
	СтрокаИнфоПроКолонки.ИмяКолонки = ИмяКолонки;
	СтрокаИнфоПроКолонки.ПредставлениеКолонки = Представление;
	СтрокаИнфоПроКолонки.ТипКолонки = ОписаниеТипа;
	СтрокаИнфоПроКолонки.ОбязательнаДляЗаполнения = ОбязательнаДляЗаполнения;
	СтрокаИнфоПроКолонки.Позиция = Позиция;
	СтрокаИнфоПроКолонки.Видимость = Истина;
	СтрокаИнфоПроКолонки.Примечание = ТекстПримечания;
	СтрокаИнфоПроКолонки.Ширина = ШиринаКолонки;
	
КонецПроцедуры

// Формирует ячейку макета по переданным параметрам 
//
Процедура ЗаполнитьЯчейкуЗаголовкаМакета(Ячейка, Текст, Ширина, Подсказка, ОбязательноеПоле, Имя = "") Экспорт
	
	Ячейка.ТекущаяОбласть.Текст = Текст;
	Ячейка.ТекущаяОбласть.Имя = Имя;
	Ячейка.ТекущаяОбласть.ПараметрРасшифровки = Имя;
	Ячейка.ТекущаяОбласть.ЦветФона =  ЦветаСтиля.ЦветФонаШапкиОтчета;
	Ячейка.ТекущаяОбласть.ШиринаКолонки = Ширина;
	Ячейка.ТекущаяОбласть.Примечание.Текст = Подсказка;
	Если ОбязательноеПоле Тогда 
		Ячейка.ТекущаяОбласть.Шрифт = Новый Шрифт(,,Истина);	
	Иначе
		Ячейка.ТекущаяОбласть.Шрифт = Новый Шрифт(,,Ложь);	
	КонецЕсли;
	
КонецПроцедуры	

// Заполняет таблицу с полной информаций о колонках в макете. Информация используется для построения таблицы сопоставления
Процедура СоздатьИнформациюПоКолонкамНаОснованиеШаблона(ОбластьЗаголовокТаблицы, ПараметрыЗагрузкиИзФайла, ИнформацияПоКолонкам) Экспорт 
	
	ИнформацияПоКолонкам.Очистить();
	Если ПараметрыЗагрузкиИзФайла <> Неопределено Тогда 
		СоответствиеТипаДанныхКолонок = ПараметрыЗагрузкиИзФайла.ТипДанныхКолонки;
	Иначе
		СоответствиеТипаДанныхКолонок = новый Соответствие;
	КонецЕсли;
	
	Для НомерКолонки = 1 по ОбластьЗаголовокТаблицы.ШиринаТаблицы Цикл
		Ячейка = ОбластьЗаголовокТаблицы.ПолучитьОбласть(1, НомерКолонки, 1, НомерКолонки).ТекущаяОбласть;
		
		Если Найти(Ячейка.Имя, "R") > 0 И Найти(Ячейка.Имя, "C") > 0 Тогда 
			ИмяРеквизита = ?(ЗначениеЗаполнено(Ячейка.ПараметрРасшифровки), Ячейка.ПараметрРасшифровки, Ячейка.Текст);
			ПредставлениеРеквизита = ?(ЗначениеЗаполнено(Ячейка.Текст), Ячейка.Текст, Ячейка.ПараметрРасшифровки);
			Ассоциация = ?(ЗначениеЗаполнено(Ячейка.ПараметрРасшифровки), Ячейка.ПараметрРасшифровки, Ячейка.Текст);
		Иначе	
			ИмяРеквизита = Ячейка.Имя;
			ПредставлениеРеквизита = ?(ЗначениеЗаполнено(Ячейка.Текст), Ячейка.Текст, Ячейка.Имя);
			Ассоциация = ?(ЗначениеЗаполнено(Ячейка.ПараметрРасшифровки), Ячейка.ПараметрРасшифровки, Ячейка.Имя);
		КонецЕсли;
		
		ТипДанныхКолонки = Новый ОписаниеТипов("Строка");
		Если СоответствиеТипаДанныхКолонок <> Неопределено Тогда 
			ТипДанныхКолонкиПереопределенный = СоответствиеТипаДанныхКолонок.Получить(ИмяРеквизита);
			Если ТипДанныхКолонкиПереопределенный <> Неопределено Тогда 
				ТипДанныхКолонки = ТипДанныхКолонкиПереопределенный;
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ИмяРеквизита) Тогда
			СтрокаИнфоПроКолонки = ИнформацияПоКолонкам.Добавить();
			СтрокаИнфоПроКолонки.ИмяКолонки = ИмяРеквизита;
			СтрокаИнфоПроКолонки.ПредставлениеКолонки = ПредставлениеРеквизита;
			СтрокаИнфоПроКолонки.ТипКолонки = ТипДанныхКолонки;
			СтрокаИнфоПроКолонки.ОбязательнаДляЗаполнения = Ячейка.Шрифт.Жирный;
			СтрокаИнфоПроКолонки.Позиция = НомерКолонки;
			СтрокаИнфоПроКолонки.Ассоциация = ?(ЗначениеЗаполнено(Ассоциация), Ассоциация, ИмяРеквизита);
			СтрокаИнфоПроКолонки.Видимость = Истина;
			СтрокаИнфоПроКолонки.Примечание = Ячейка.Примечание.Текст;
			СтрокаИнфоПроКолонки.Ширина = Ячейка.ШиринаКолонки;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьИнформациюПоКолонке(ИнформацияПоКолонкам, Имя, Представление, Тип, ОбязательнаДляЗаполнения, Позиция, Ассоциация = "")
	СтрокаИнфоПроКолонки = ИнформацияПоКолонкам.Добавить();
	СтрокаИнфоПроКолонки.ИмяКолонки = Имя;
	СтрокаИнфоПроКолонки.ПредставлениеКолонки = Представление;
	СтрокаИнфоПроКолонки.ТипКолонки = Тип;
	СтрокаИнфоПроКолонки.ОбязательнаДляЗаполнения = ОбязательнаДляЗаполнения;
	СтрокаИнфоПроКолонки.Позиция = Позиция;
	СтрокаИнфоПроКолонки.Ассоциация = ?(ЗначениеЗаполнено(Ассоциация), Ассоциация, Имя);
	СтрокаИнфоПроКолонки.Видимость = Истина;
КонецПроцедуры

// Создает таблицу значений по данным из шаблона и сохраняет ее во временное хранилище 
//
Процедура ВыгрузитьДанныеДляТЧ(ШаблонСДанными, ИнформацияПоКолонкам, АдресЗагруженныхДанных) Экспорт
	
	ЗагружаемыеДанные = Новый ТаблицаЗначений;
	
	ОбластьЗаголовка = ОбластьЗаголовкаШаблонаТаблицы(ШаблонСДанными);
	
	Для каждого Колонка Из ИнформацияПоКолонкам Цикл 
		ЗагружаемыеДанные.Колонки.Добавить(Колонка.ИмяКолонки, Новый ОписаниеТипов("Строка"), Колонка.ПредставлениеКолонки);
	КонецЦикла;
	ОписаниеТипаЧисло = Новый ОписаниеТипов("Число");
	ОписаниеТипаСтрока = Новый ОписаниеТипов("Строка");
	ЗагружаемыеДанные.Колонки.Добавить("Идентификатор",ОписаниеТипаЧисло, "Идентификатор");
	ЗагружаемыеДанные.Колонки.Добавить("РезультатСопоставленияСтроки",ОписаниеТипаСтрока, "Результат");
	ЗагружаемыеДанные.Колонки.Добавить("ОписаниеОшибки",ОписаниеТипаСтрока, "Причина");
	
	Для НомерСтроки = 2 По ШаблонСДанными.ВысотаТаблицы Цикл 
		НоваяСтрока = ЗагружаемыеДанные.Добавить();
		НоваяСтрока.Идентификатор =  НомерСтроки - 1;
		Для НомерКолонки = 1 По ШаблонСДанными.ШиринаТаблицы Цикл 
			Ячейка = ШаблонСДанными.ПолучитьОбласть(НомерСтроки,НомерКолонки,НомерСтроки,НомерКолонки).ТекущаяОбласть;
			
			НайденнаяКолонка = НайтиИнформациюОКолонке(ИнформацияПоКолонкам, "Позиция", НомерКолонки);

			Если НайденнаяКолонка <> Неопределено Тогда 
				ИмяКолонки = НайденнаяКолонка.ИмяКолонки; 
				НоваяСтрока[ИмяКолонки] = Ячейка.Текст; 
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	АдресЗагруженныхДанных = ПоместитьВоВременноеХранилище(ЗагружаемыеДанные);
КонецПроцедуры

//  Заполняет информацию по колонкам и формирует шаблон по табличной части документа
//
Процедура ИнициализироватьЗагрузкуВТабличнуюЧасть(ПолноеИмяТабличнойЧасти, ИмяМакетаСШаблоном, ИнформацияПоКолонкам, ШаблонСДанными, Отказ) Экспорт
	ИмяОбъекта = РазложитьПолноеИмяОбъекта(ПолноеИмяТабличнойЧасти);
	
	Попытка
		ПараметрыЗагрузкиИзФайла = ПараметрыЗагрузкиИзФайлаВТЧ(ПолноеИмяТабличнойЧасти);
	Исключение
	КонецПопытки;
	
	Если ИмяОбъекта.ТипОбъекта = "Документ" Тогда 
		МенеджерОбъекта = Документы[ИмяОбъекта.НазваниеОбъекта];
		МетаданныеТЧ = Метаданные.Документы[ИмяОбъекта.НазваниеОбъекта].ТабличныеЧасти[ИмяОбъекта.ИмяТабличнойЧасти];
		
		Попытка
			МенеджерОбъекта.УстановитьПараметрыЗагрузкиИзФайлаВТЧ(ПараметрыЗагрузкиИзФайла);
		Исключение
		КонецПопытки;
		
		ПараметрыЗагрузки = ПараметрыЗагрузкиИзФайла;
		МакетМетаданные = Метаданные.Документы[ИмяОбъекта.НазваниеОбъекта].Макеты.Найти(ИмяМакетаСШаблоном);
		
		МакетМетаданные= Метаданные.Документы[ИмяОбъекта.НазваниеОбъекта].Макеты.Найти("ЗагрузкаИзФайла"+ИмяОбъекта.ИмяТабличнойЧасти);
		Если МакетМетаданные = Неопределено тогда 
			МакетМетаданные = Метаданные.Документы[ИмяОбъекта.НазваниеОбъекта].Макеты.Найти("ЗагрузкаИзФайла");
		КонецЕсли;
		
		Если МакетМетаданные <> Неопределено тогда
			Макет = МенеджерОбъекта.ПолучитьМакет(МакетМетаданные.Имя);
		Иначе
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		ЗаголовокТаблицы = ОбластьЗаголовкаШаблонаТаблицы(Макет);
		СоздатьИнформациюПоКолонкамНаОснованиеШаблона(ЗаголовокТаблицы, Неопределено, ИнформацияПоКолонкам);
		
		Для каждого Реквизит Из МетаданныеТЧ.Реквизиты Цикл
			
			Колонка = НайтиИнформациюОКолонке(ИнформацияПоКолонкам, "ИмяКолонки", Реквизит.Имя);
			
			Если Колонка <> Неопределено тогда
				Колонка.ТипКолонки = Реквизит.Тип;
				Если Реквизит.ПроверкаЗаполнения = ПроверкаЗаполнения.ВыдаватьОшибку тогда
					Колонка.ОбязательнаДляЗаполнения = Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;	
		
		ШаблонСДанными.Вывести(ЗаголовокТаблицы);
	КонецЕсли; 
	
КонецПроцедуры

// Создает шапку бланка по информации о колонках
//
Функция ШапкаБланкаДляЗаполненияПоИнформацииПоКолонкам(ИнформацияПоКолонкам, СПримечаниями = Истина) Экспорт

	ТабличныйДокумент = новый ТабличныйДокумент;
	
	ПростойШаблон = Обработки.ЗагрузкаДанныхИзФайла.ПолучитьМакет("ПростойШаблон");
	ОбластьЗаголовок = ПростойШаблон.ПолучитьОбласть("Заголовок");
	ИнформацияПоКолонкам.Сортировать("Позиция");
	
	Для Позиция = 0 По ИнформацияПоКолонкам.Количество() -1 Цикл
		Колонка = ИнформацияПоКолонкам.Получить(Позиция);
		
		Если Колонка.Видимость = Истина Тогда
			ОбластьЗаголовок.ТекущаяОбласть.Имя = Колонка.ИмяКолонки;
			ОбластьЗаголовок.ТекущаяОбласть.Расшифровка = Колонка.Ассоциация;
			Если ЗначениеЗаполнено(Колонка.Синоним) Тогда 
				ОбластьЗаголовок.Параметры.Заголовок = Колонка.Синоним;
			Иначе
				ОбластьЗаголовок.Параметры.Заголовок = Колонка.ПредставлениеКолонки;
			КонецЕсли;
			Если Колонка.ОбязательнаДляЗаполнения Тогда
				ОбластьЗаголовок.ТекущаяОбласть.Шрифт = Новый Шрифт(,, Истина);
			Иначе
				ОбластьЗаголовок.ТекущаяОбласть.Шрифт = Новый Шрифт(,, Ложь);
			КонецЕсли;
			Если Колонка.Ширина = 0 Тогда 
				ОбластьЗаголовок.ТекущаяОбласть.ШиринаКолонки = ШиринаКолонкиПоТипу(Колонка.ТипКолонки);
			Иначе
				ОбластьЗаголовок.ТекущаяОбласть.ШиринаКолонки = Колонка.Ширина; 
			КонецЕсли;
			Если СПримечаниями Тогда
				ОбластьЗаголовок.ТекущаяОбласть.Примечание.Текст = Колонка.Примечание;
			КонецЕсли;
			ТабличныйДокумент.Присоединить(ОбластьЗаголовок);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
КонецФункции

// Возвращает полное имя объекта в виде структуры 
//
Функция ПолноеИмяОбъектаТабличнаяЧасть(ИмяОбъекта) Экспорт
	
	Результат = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИмяОбъекта,".");
	Если Результат.Количество() = 4 тогда
			Объект = Метаданные.НайтиПоПолномуИмени(ИмяОбъекта);
			Если Объект <> Неопределено Тогда 
				Возврат ИмяОбъекта;
			КонецЕсли;
	ИначеЕсли Результат.Количество() = 3 тогда
		Если Результат[2] <> "ТабличнаяЧасть" Тогда 
			ИмяОбъекта = Результат[0] + "." + Результат[1] + ".ТабличнаяЧасть." + Результат[2];
			Объект = Метаданные.НайтиПоПолномуИмени(ИмяОбъекта);
			Если Объект <> Неопределено Тогда 
				Возврат ИмяОбъекта;
			КонецЕсли;
		ИначеЕсли Результат[1] = "ТабличнаяЧасть" Тогда 
			ИмяОбъекта = "Документ." + Результат[0] + ".ТабличнаяЧасть." + Результат[2];
			Объект = Метаданные.НайтиПоПолномуИмени(ИмяОбъекта);
			Если Объект <> Неопределено Тогда 
				Возврат ИмяОбъекта;
			КонецЕсли;
			ИмяОбъекта = "Справочник." + Результат[0] + ".ТабличнаяЧасть." + Результат[2];
			Объект = Метаданные.НайтиПоПолномуИмени(ИмяОбъекта);
			Если Объект <> Неопределено Тогда 
				Возврат ИмяОбъекта;
			КонецЕсли;
			Возврат Неопределено;
		КонецЕсли;
		
		Возврат Неопределено;
	ИначеЕсли Результат.Количество() = 2 тогда
		Если Результат[0] <> "Документ" или Результат[0] <> "Справочник" Тогда 
			ИмяОбъекта = "Документ." + Результат[0] + ".ТабличнаяЧасть." + Результат[1];
			Объект = Метаданные.НайтиПоПолномуИмени(ИмяОбъекта);
			Если Объект <> Неопределено Тогда 
				Возврат ИмяОбъекта;
			КонецЕсли;
			ИмяОбъекта = "Справочник." + Результат[0] + ".ТабличнаяЧасть." + Результат[1];
			Объект = Метаданные.НайтиПоПолномуИмени(ИмяОбъекта);
			Если Объект <> Неопределено Тогда 
				Возврат ИмяОбъекта;
			КонецЕсли;
			Возврат Неопределено;
		КонецЕсли;
		НазваниеОбъектаМетаданных = Результат[0];
		ИмяТабличнойЧасти = Результат[1];
		ТипОбъектаМетаданных = Метаданные.Справочники.Найти(НазваниеОбъектаМетаданных);
		Если ТипОбъектаМетаданных <> Неопределено Тогда 
			ТипОбъектаМетаданных = "Справочник";
		Иначе
			ТипОбъектаМетаданных = Метаданные.Документы.Найти(НазваниеОбъектаМетаданных);					
			Если ТипОбъектаМетаданных <> Неопределено Тогда 
				ТипОбъектаМетаданных = "Документ";
			Иначе 
				Возврат Неопределено;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Неопределено;	
	
КонецФункции

// Возвращает имя объекта в виде структуры
//
// Параметры:
//	ПолноеИмяОбъекта - Структура - Имя объекта
//		* ТипОбъекта - Строка - Тип объекта
//		* НазваниеОбъекта - Строка - Название объекта
//		* ИмяТабличнойЧасти - Строка - Имя табличной части
Функция РазложитьПолноеИмяОбъекта(ПолноеИмяОбъекта) Экспорт
	Результат = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ПолноеИмяОбъекта, ".");
	
	ИмяОбъекта = Новый Структура;
	ИмяОбъекта.Вставить("ТипОбъекта");
	ИмяОбъекта.Вставить("НазваниеОбъекта");
	ИмяОбъекта.Вставить("ИмяТабличнойЧасти");
	
	Если Результат.Количество() = 2 Тогда
		Если Результат[0] = "Документ" ИЛИ Результат[0] = "Справочник" ИЛИ Результат[0] = "БизнесПроцесс" ИЛИ
			Результат[0] = "Перечисление" ИЛИ Результат[0] = "ПланВидовХарактеристик" ИЛИ Результат[0] = "ПланСчетов" Тогда
			ИмяОбъекта.ТипОбъекта = Результат[0];
			ИмяОбъекта.НазваниеОбъекта = Результат[1];
		Иначе
			 ИмяОбъекта.ТипОбъекта = ОпределитьТипОбъектаМетаданныхПоИмени(Результат[0]);
			 ИмяОбъекта.НазваниеОбъекта = Результат[0];
			 ИмяОбъекта.ИмяТабличнойЧасти = Результат[1];
		КонецЕсли;
	ИначеЕсли Результат.Количество() = 3 Тогда
		ИмяОбъекта.ТипОбъекта = Результат[0];
		ИмяОбъекта.НазваниеОбъекта = Результат[1];
		ИмяОбъекта.ИмяТабличнойЧасти = Результат[2];
	ИначеЕсли Результат.Количество() = 4 Тогда 
		ИмяОбъекта.ТипОбъекта = Результат[0];
		ИмяОбъекта.НазваниеОбъекта = Результат[1];
		ИмяОбъекта.ИмяТабличнойЧасти = Результат[3];
	ИначеЕсли Результат.Количество() = 1 Тогда
		ИмяОбъекта.ТипОбъекта = ОпределитьТипОбъектаМетаданныхПоИмени(Результат[0]);
		ИмяОбъекта.НазваниеОбъекта = Результат[0];
	КонецЕсли;

	Возврат ИмяОбъекта;
	
КонецФункции

Функция ОпределитьТипОбъектаМетаданныхПоИмени(Имя)
	Для каждого Объект Из Метаданные.Справочники Цикл 
		Если Объект.Имя = Имя Тогда 
			Возврат "Справочник";
		КонецЕсли;
	КонецЦикла;
	
	Для каждого Объект Из Метаданные.Документы Цикл 
		Если Объект.Имя = Имя Тогда 
			Возврат "Документ";
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
КонецФункции

// Загружает данные из файла в таблицу
//
Процедура ЗаполнитьТаблицуПоЗагруженнымДаннымИзФайла(ДанныеИзФайла, ШаблонСДанными, ИнформацияПоКолонкам) Экспорт 
	
	СтрокаЗаголовок= ДанныеИзФайла.Получить(0);
	СоответствиеКолонок = Новый Соответствие;
	
	Для каждого Колонка Из ДанныеИзФайла.Колонки Цикл
		НайденнаяКолонка = НайтиИнформациюОКолонке(ИнформацияПоКолонкам, "Синоним", СтрокаЗаголовок[Колонка.Имя]);
		Если НайденнаяКолонка = Неопределено Тогда
			НайденнаяКолонка = НайтиИнформациюОКолонке(ИнформацияПоКолонкам, "ПредставлениеКолонки", СтрокаЗаголовок[Колонка.Имя]);
		КонецЕсли;
		Если НайденнаяКолонка <> Неопределено Тогда 
			СоответствиеКолонок.Вставить(НайденнаяКолонка.Позиция, Колонка.Имя);
		КонецЕсли;
	КонецЦикла;
	
	Для Индекс= 1 По ДанныеИзФайла.Количество() - 1 Цикл
		СтрокаТЗ = ДанныеИзФайла.Получить(Индекс);
		
		Для НомерКолонки =1 По ШаблонСДанными.ШиринаТаблицы Цикл
			КолонкаВТаблице = СоответствиеКолонок.Получить(НомерКолонки);
			Колонка = ИнформацияПоКолонкам.Найти(НомерКолонки, "Позиция");
			Если Колонка <> Неопределено И Колонка.Видимость = Ложь Тогда
				Продолжить;
			КонецЕсли;
			Ячейка = ШаблонСДанными.ПолучитьОбласть(2, НомерКолонки, 2, НомерКолонки);
			Если КолонкаВТаблице <> Неопределено тогда 
				Ячейка.ТекущаяОбласть.Текст = СтрокаТЗ[КолонкаВТаблице];
				Ячейка.ТекущаяОбласть.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Обрезать;
			Иначе
				Ячейка.ТекущаяОбласть.Текст = "";
			КонецЕсли;
			Если НомерКолонки = 1 Тогда
				ШаблонСДанными.Вывести(Ячейка);
			иначе
				ШаблонСДанными.Присоединить(Ячейка);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

// Загружает файл в таблицу
//
Процедура ЗагрузитьФайлВТаблицу(ПараметрыВызоваСервера, АдресХранилища) Экспорт
	
	Расширение = ПараметрыВызоваСервера.Расширение;
	ШаблонСДанными = ПараметрыВызоваСервера.ШаблонСДанными;
	ИмяВременногоФайла = ПараметрыВызоваСервера.ИмяВременногоФайла;
	ИнформацияПоКолонкам = ПараметрыВызоваСервера.ИнформацияПоКолонкам;
	
	Если Расширение = "xlsx" Тогда 
		ЗагрузитьФайлExcel2007ВТаблицу(ИмяВременногоФайла, ШаблонСДанными, ИнформацияПоКолонкам);
	ИначеЕсли Расширение = "csv" Тогда 
		ЗагрузитьCSVФайлВТаблицу(ИмяВременногоФайла, ШаблонСДанными, ИнформацияПоКолонкам);
	Иначе
		ШаблонСДанными.Прочитать(ИмяВременногоФайла);
	КонецЕсли;
	
	АдресХранилища = ПоместитьВоВременноеХранилище(ШаблонСДанными, АдресХранилища);
	
КонецПроцедуры

#Область ЗагрузкаФайловФорматаExcel2007

// Загружает файл формата Excel 2007 в шаблон 
//
Процедура ЗагрузитьФайлExcel2007ВТаблицу(ПутьКФайлу, ШаблонСДанными, ИнформацияПоКолонкам) Экспорт
	Файл = Новый Файл(ПутьКФайлу);
	Если НЕ Файл.Существует() Тогда
		Возврат;
	КонецЕсли;
	
	ФайлЭкзель = ПутьКФайлу;
	ВременныйКаталог = КаталогВременныхФайлов() + ПолучитьРазделительПути() + "excel2007";
	УдалитьФайлы(ВременныйКаталог);
	
	РаспаковатьФайл(ФайлЭкзель, ВременныйКаталог);
	
	ФайлСтрок = ВременныйКаталог + ПолучитьРазделительПути() + "xl" + ПолучитьРазделительПути() +"sharedStrings.xml";
	СписокСтрок = ПрочитатьСписокСтрок(ФайлСтрок);
	
	ФайлФорматов = ВременныйКаталог + ПолучитьРазделительПути() + "xl" + ПолучитьРазделительПути() +"styles.xml";
	СписокФорматов = ПрочитатьСписокФорматов(ФайлФорматов);
	
	НомерЛиста = 1;
	ФайлЛиста = ВременныйКаталог + ПолучитьРазделительПути() + "xl" + ПолучитьРазделительПути() + "worksheets" + ПолучитьРазделительПути() + "sheet" + НомерЛиста + ".xml";
	Файл = Новый Файл(ФайлЛиста);
	Если НЕ Файл.Существует() Тогда
		Возврат;
	КонецЕсли;
	
	МассивБукв = ПолучитьМассивБукв();
	ДеревоДанных = ПолучитьДеревоДанных(ФайлЛиста);
	
	Таблица = Новый ТаблицаЗначений;
	
	//создаем колонки
	Колонки = ДеревоДанных.Строки.Найти("dimension", "Объект", Истина);
	Счетчик = 0;
	Для Каждого Строка Из Колонки.Строки Цикл
		Если Строка.Объект = "ref" Тогда
			Диапозон = Строка.Значение; 
			//поиск максимального значения колонки
			Счетчик = МассивБукв.Количество();
			Пока Счетчик > 0 Цикл 
				Счетчик = Счетчик - 1;
				Если Найти(Диапозон, МассивБукв[Счетчик]) > 0 Тогда
					Для Индекс = 0 По Счетчик Цикл
						Таблица.Колонки.Добавить(МассивБукв[Индекс]);
					КонецЦикла;
					Счетчик = 0;
				КонецЕсли;
			КонецЦикла;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	//читаем строки
	СтрСтрок = ДеревоДанных.Строки.Найти("sheetData", "Объект", Истина);
	Для Каждого Строка Из СтрСтрок.Строки Цикл
		НоваяСтрока = Таблица.Добавить();
		
		Для Каждого Колонка Из Строка.Строки Цикл
			Если Колонка.Объект <> "c" Тогда
				Продолжить;
			КонецЕсли;
			
			ЗначениеЯчейки = Неопределено;
			
			СтрЗначение = Колонка.Строки.Найти("v", "Объект");
			Если СтрЗначение <> Неопределено Тогда
				ЗначениеЯчейки = СтрЗначение.Значение;
			КонецЕсли;
			
			СтрЗначение = Колонка.Строки.Найти("t", "Объект");
			Если СтрЗначение <> Неопределено И СтрЗначение.Значение = "s" И ЗначениеЯчейки <> Неопределено Тогда
				Позиция = Число(ЗначениеЯчейки); 
				Если СписокСтрок.Количество() > Позиция Тогда
					ЗначениеЯчейки = СписокСтрок.Получить(Позиция).Значение;
				КонецЕсли;
			КонецЕсли;

			
			сОбъект = Колонка.Строки.Найти("s", "Объект");
			Если сОбъект <> Неопределено Тогда
				СтрЗначение = СтрокуВЧисло(Колонка.Строки.Найти("s", "Объект").Значение, -1);
				Если СтрЗначение >= 0 Тогда
					ИмяФормата = СписокФорматов.Получить(СтрЗначение);
					Если ИмяФормата = "Дата" ИЛИ ИмяФормата = "ДатаВремя" ИЛИ ИмяФормата = "Время" Тогда
						ПозицияРазделителя = Найти(ЗначениеЯчейки, ".");
						Если ПозицияРазделителя > 0 Тогда 
							КоличествоДней = СтрокуВЧисло(Лев(ЗначениеЯчейки, ПозицияРазделителя - 1)) * 86400 - 2 * 86400;
							КоличествоСекунд = СтрокуВЧисло(Сред(ЗначениеЯчейки, ПозицияРазделителя + 1)) - 2 * 60;
						Иначе
							КоличествоДней = СтрокуВЧисло(ЗначениеЯчейки) * 86400 - 2 * 86400;
							КоличествоСекунд = 0;
						КонецЕсли;
						ПолученнаяДата = Дата(1900, 1, 1, 0, 0, 0) + КоличествоДней + КоличествоСекунд;
						Если ИмяФормата = "Дата" Тогда 
							ЗначениеЯчейки = Формат(ПолученнаяДата, "ДЛФ=D");
						ИначеЕсли ИмяФормата = "ДатаВремя" Тогда 
							ЗначениеЯчейки = Формат(ПолученнаяДата, "ДЛФ=DT");
						ИначеЕсли ИмяФормата = "Время" Тогда 
							ЗначениеЯчейки = Формат(ПолученнаяДата, "ДЛФ=T");
						КонецЕсли;
					ИначеЕсли ИмяФормата = "Число" Тогда
						ЗначениеЯчейки = СтрокуВЧисло(ЗначениеЯчейки);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;

			//поиск колонки
			СтрЗначение = Колонка.Строки.Найти("r", "Объект");
			Если СтрЗначение <> Неопределено Тогда
				ИмяКолонки = СтрЗначение.Значение;
			КонецЕсли;
			ИндексСтроки = Неопределено;
			Счетчик = МассивБукв.Количество();
			Пока Счетчик > 0 Цикл 
				Счетчик = Счетчик - 1;
				Если Найти(ИмяКолонки, МассивБукв[Счетчик])>0 Тогда
					ИндексСтроки = Счетчик;
					Счетчик = 0;
				КонецЕсли;   
			КонецЦикла;
			
			НоваяСтрока[МассивБукв[ИндексСтроки]] = ЗначениеЯчейки;
		КонецЦикла;
	КонецЦикла;
	
	ЗаполнитьТаблицуПоЗагруженнымДаннымИзФайла(Таблица, ШаблонСДанными, ИнформацияПоКолонкам);
	
КонецПроцедуры

Процедура РаспаковатьФайл(Файл, Каталог)
	Зип = Новый ЧтениеZipФайла;
	Зип.Открыть(Файл);
	Зип.ИзвлечьВсе(Каталог, РежимВосстановленияПутейФайловZIP.Восстанавливать);
КонецПроцедуры

Функция ПолучитьМассивБукв()
	МассивБукв = Новый Массив;
	МассивБукв.Добавить("A");
	МассивБукв.Добавить("B");
	МассивБукв.Добавить("C");
	МассивБукв.Добавить("D");
	МассивБукв.Добавить("E");
	МассивБукв.Добавить("F");
	МассивБукв.Добавить("G");
	МассивБукв.Добавить("H");
	МассивБукв.Добавить("I");
	МассивБукв.Добавить("J");
	МассивБукв.Добавить("K");
	МассивБукв.Добавить("L");
	МассивБукв.Добавить("M");
	МассивБукв.Добавить("N");
	МассивБукв.Добавить("O");
	МассивБукв.Добавить("P");
	МассивБукв.Добавить("Q");
	МассивБукв.Добавить("R");
	МассивБукв.Добавить("S");
	МассивБукв.Добавить("T");
	МассивБукв.Добавить("U");
	МассивБукв.Добавить("V");
	МассивБукв.Добавить("W");
	МассивБукв.Добавить("X");
	МассивБукв.Добавить("Y");
	МассивБукв.Добавить("Z");
	
	Возврат МассивБукв;
КонецФункции

Функция ПрочитатьСписокСтрок(ФайлСтрок)
	Строки = Новый СписокЗначений;
	
	Хмл = Новый ЧтениеXML;
	Хмл.ОткрытьФайл(ФайлСтрок);
	
	Пока Хмл.Прочитать() Цикл
		Если Хмл.ТипУзла = ТипУзлаXML.Текст Тогда
			Строки.Добавить(Хмл.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Хмл.Закрыть();
	
	Возврат Строки;
КонецФункции

Функция ПолучитьДеревоДанных(Файл)
	
	ДеревоДанных = Новый ДеревоЗначений;
	ДеревоДанных.Колонки.Добавить("Объект");
	ДеревоДанных.Колонки.Добавить("Значение");
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ОткрытьФайл(Файл);
	
	ТекЭлемент = Неопределено;
	ТекБаза = Неопределено;
	
	Пока ЧтениеXML.Прочитать() Цикл
		Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			РежимЗагрузки = ЧтениеXML.Имя;
			
			Если ТекЭлемент = Неопределено Тогда
				ТекЭлемент = ДеревоДанных.Строки.Добавить();
				ТекЭлемент.Объект = РежимЗагрузки;
			Иначе
				ТекЭлемент = ТекЭлемент.Строки.Добавить();
				ТекЭлемент.Объект = РежимЗагрузки;					
			КонецЕсли;
			
		ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
			Если ТекЭлемент = Неопределено Тогда
				ТекЭлемент = Неопределено;
			Иначе
				ТекЭлемент = ТекЭлемент.Родитель;
			КонецЕсли;
		ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.Текст Тогда
			ТекЭлемент.Значение = ЧтениеXML.Значение;
		КонецЕсли;
		
		Для Индекс = 0 По ЧтениеXML.КоличествоАтрибутов() - 1 Цикл
			Строка = ТекЭлемент.Строки.Добавить();
			Строка.Объект = ЧтениеXML.ИмяАтрибута(Индекс);
			Строка.Значение = ЧтениеXML.ЗначениеАтрибута(Индекс);
		КонецЦикла;
	КонецЦикла;
	
	ЧтениеXML.Закрыть();
	
	Возврат ДеревоДанных;
КонецФункции

Функция ПрочитатьСписокФорматов(ФайлФорматов)
	
	Хмл = Новый ЧтениеXML;
	Хмл.ОткрытьФайл(ФайлФорматов);
	ОписаниеФорматов = Новый Соответствие;
	Позиция = -1;
	
	Пока Хмл.Прочитать() Цикл
		Если Хмл.ТипУзла = ТипУзлаXML.НачалоЭлемента и Хмл.Имя = "xf" Тогда
			// Описание формата даты http://social.msdn.microsoft.com/Forums/office/en-US/e27aaf16-b900-4654-8210-83c5774a179c/xlsx-numfmtid-predefined-id-14-doesnt-match?forum=oxmlsdk
			Если Хмл.ЗначениеАтрибута("numFmtId") <> Неопределено Тогда 
				НомерФормата =  Число(Хмл.ЗначениеАтрибута("numFmtId"));
				Если НомерФормата > 0 И НомерФормата < 12 Тогда
					ОписаниеФорматов.Вставить(Позиция, "Число");
				ИначеЕсли НомерФормата > 13 И НомерФормата <= 17 Тогда
					ОписаниеФорматов.Вставить(Позиция, "Дата");
				ИначеЕсли НомерФормата >= 18 И НомерФормата <= 21 Тогда
					ОписаниеФорматов.Вставить(Позиция, "Время");
				ИначеЕсли НомерФормата = 22 ИЛИ НомерФормата = 165 ИЛИ НомерФормата = 166 Тогда
					ОписаниеФорматов.Вставить(Позиция, "ДатаВремя");
				Иначе 
					ОписаниеФорматов.Вставить(Позиция, "Строка");
				КонецЕсли;
			КонецЕсли;
			Позиция = Позиция + 1;
		КонецЕсли;
	КонецЦикла;
	
	Хмл.Закрыть();
	
	Возврат ОписаниеФорматов;
	
КонецФункции

// Превращает строку в число без вызова исключений. Стандартная функция преобразования
//   Число() строго контролирует отсутствие каких либо символов кроме числовых.
//
Функция СтрокуВЧисло(Значение, ЗначениеПоУмолчанию = 0)
	
	Если Значение ="0" Тогда
		// Представление значения по умолчанию обрабатываем отдельно
		Результат = 0
	Иначе
		СтепеньЧисла = 0;
		Позиция = Найти(Значение, "E");
		Если Позиция > 0 Тогда
			СтепеньЧисла = Сред(Значение, Позиция + 1);
			Значение = Лев(Значение, Позиция - 1);
		КонецЕсли;
		
		ЦелевойТип = Новый ОписаниеТипов("Число");
		Результат = ЦелевойТип.ПривестиЗначение(Значение);
		
		Если СтепеньЧисла <> 0 Тогда
			Результат = Результат * Pow(10, СтепеньЧисла);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ?(Результат = 0, ЗначениеПоУмолчанию, Результат);
КонецФункции

#КонецОбласти 

#Область РаботаСCSVФайлами 

// Загружает файл формата CSV в шаблон 
//
Процедура ЗагрузитьCSVФайлВТаблицу(ИмяФайла, ШаблонСДанными, ИнформацияПоКолонкам) Экспорт
	
	Файл = новый Файл(ИмяФайла);
	Если НЕ Файл.Существует() Тогда 
		Возврат;
	КонецЕсли;
	
	ЧтениеТекста = Новый ЧтениеТекста(ИмяФайла);
	Строка = ЧтениеТекста.ПрочитатьСтроку();
	Если Строка = Неопределено Тогда 
		ТекстСообщения = НСтр("ru = 'Не получилось загрузить данные из этого файла. Убедитесь в корректности данных в файле.'");
		Возврат;
	КонецЕсли;
	
	КолонкиШапки = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивСлов(Строка, ";");
	Источник = Новый ТаблицаЗначений;
	
	
	Для каждого Колонка Из КолонкиШапки Цикл 
		
		НайденнаяКолонка = НайтиИнформациюОКолонке(ИнформацияПоКолонкам, "ПредставлениеКолонки", Колонка);
		
		Если НайденнаяКолонка <> Неопределено Тогда 
			НоваяКолонка = Источник.Колонки.Добавить();
			НоваяКолонка.Имя = НайденнаяКолонка.ИмяКолонки; 
			НоваяКолонка.Заголовок = Колонка;	
		КонецЕсли;
	КонецЦикла;
	
	Если Источник.Колонки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Пока Строка <> Неопределено Цикл 
		НоваяСтрока = Источник.Добавить();
		Позиция = Найти(Строка, ";");
		Индекс = 0;
		Пока Позиция > 0 Цикл
			Если Источник.Колонки.Количество() < Индекс + 1  Тогда 
				Прервать;
			КонецЕсли;
			НоваяСтрока[Индекс] = Лев(Строка, Позиция - 1);
			Строка = Сред(Строка, Позиция + 1);
			Позиция = Найти(Строка, ";");
			Индекс = Индекс + 1; 
		КонецЦикла;
		Если Источник.Колонки.Количество() = Индекс + 1  Тогда 
			НоваяСтрока[Индекс] = Строка;
		КонецЕсли;

		Строка = ЧтениеТекста.ПрочитатьСтроку();
	КонецЦикла;
	
	ЗаполнитьТаблицуПоЗагруженнымДаннымИзФайла(Источник, ШаблонСДанными, ИнформацияПоКолонкам);
	
КонецПроцедуры

// Сохраняет файл формата CSV в шаблон 
//
Процедура СохранитьТаблицуВCSVФайл(ПутьКФайлу, ИнформацияПоКолонкам) Экспорт
	
	ФорматЗаголовкаДляCSV = "";
	
	Для каждого Колонка Из ИнформацияПоКолонкам Цикл 
		ФорматЗаголовкаДляCSV = ФорматЗаголовкаДляCSV + Колонка.ПредставлениеКолонки + ";";
	КонецЦикла;
	
	Если СтрДлина(ФорматЗаголовкаДляCSV) > 0 Тогда
		ФорматЗаголовкаДляCSV = Лев(ФорматЗаголовкаДляCSV, СтрДлина(ФорматЗаголовкаДляCSV)-1);
	КонецЕсли;
	
	Файл = Новый ЗаписьТекста(ПутьКФайлу);
	Файл.ЗаписатьСтроку(ФорматЗаголовкаДляCSV);
	Файл.Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область ДлительныеОперации

// Сохраняет загруженные и сопоставленные  данные в справочник
//
Процедура ЗаписатьСопоставленныеДанные(ПараметрыВыгрузки, АдресХранилища) Экспорт
	
	СопоставленныеДанные = ПараметрыВыгрузки.СопоставленныеДанные;
	ИмяОбъектаСопоставления =ПараметрыВыгрузки.ИмяОбъектаСопоставления;
	ПараметрыЗагрузки = ПараметрыВыгрузки.ПараметрыЗагрузки;
	ИнформацияПоКолонкам = ПараметрыВыгрузки.ИнформацияПоКолонкам;
	
	СоздаватьЕслиНеСопоставлено = ПараметрыЗагрузки.СоздаватьЕслиНеСопоставлено;
	ОбновлятьСуществующие = ПараметрыЗагрузки.ОбновлятьСуществующие;
	
	ТипСтрока = Новый ОписаниеТипов("Строка");
	
	ИмяСправочника = РазложитьПолноеИмяОбъекта(ИмяОбъектаСопоставления).НазваниеОбъекта;
	МенеджерСправочника = Справочники[ИмяСправочника];
	
	НомерСтроки = 0;
	ВсегоСтрок = СопоставленныеДанные.Количество();
	Для каждого СтрокаТаблицы Из СопоставленныеДанные Цикл 
		НомерСтроки = НомерСтроки + 1;
		Попытка
			НачатьТранзакцию();
			Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ОбъектСопоставления) Тогда 
				Если СоздаватьЕслиНеСопоставлено Тогда 
					ЭлементСправочника = МенеджерСправочника.СоздатьЭлемент();
					СтрокаТаблицы.ОбъектСопоставления = ЭлементСправочника;
					СтрокаТаблицы.РезультатСопоставленияСтроки = "Создан";
				Иначе
					СтрокаТаблицы.РезультатСопоставленияСтроки = "Пропущен";
					УстановитьПроцентПрогресса(ВсегоСтрок, НомерСтроки);
					Продолжить;
				КонецЕсли;
			Иначе
				Если НЕ ОбновлятьСуществующие Тогда 
					СтрокаТаблицы.РезультатСопоставленияСтроки = "Пропущен";
					УстановитьПроцентПрогресса(ВсегоСтрок, НомерСтроки);
					Продолжить;
				КонецЕсли;
				
				Блокировка = Новый БлокировкаДанных;
				ЭлементБлокировки = Блокировка.Добавить("Справочник." + ИмяСправочника);
				ЭлементБлокировки.УстановитьЗначение("Ссылка", СтрокаТаблицы.ОбъектСопоставления);
				
				ЭлементСправочника = СтрокаТаблицы.ОбъектСопоставления.ПолучитьОбъект();
				СтрокаТаблицы.РезультатСопоставленияСтроки = "Обновлен";
				Если ЭлементСправочника = Неопределено Тогда
					ТексСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Номенклатура с артикулом %1 не существует.'"), СтрокаТаблицы.Артикул);
					ВызватьИсключение ТексСообщения;
				КонецЕсли;
			КонецЕсли;
			
			Для каждого Колонка из ИнформацияПоКолонкам Цикл 
				ЭлементСправочника[Колонка.ИмяКолонки] = СтрокаТаблицы[Колонка.ИмяКолонки];
			КонецЦикла;
			
			Если ЭлементСправочника.ПроверитьЗаполнение() Тогда 
				ЭлементСправочника.Записать();
				ЗафиксироватьТранзакцию();
			Иначе
				СтрокаТаблицы.РезультатСопоставленияСтроки = "Пропущен";
				СообщенияПользователю = ПолучитьСообщенияПользователю(Истина);
				
				Если СообщенияПользователю.Количество() > 0 Тогда 
					ТекстСообщений = "";
					Для каждого СообщениеПользователю Из СообщенияПользователю Цикл
						ТекстСообщений = текстСообщений + СообщениеПользователю.Текст + Символы.ПС;
					КонецЦикла;
					СтрокаТаблицы.ОписаниеОшибки = ТекстСообщений;
				КонецЕсли;
			
				ОтменитьТранзакцию();

			КонецЕсли;
			
			УстановитьПроцентПрогресса(ВсегоСтрок, НомерСтроки);
		Исключение
			ОтменитьТранзакцию();
			СтрокаТаблицы.РезультатСопоставленияСтроки = "Пропущен";
			СтрокаТаблицы.ОписаниеОшибки = НСтр("ru = 'Невозможна запись из-за некорректности данных'");
		КонецПопытки;
	
	КонецЦикла;
	
	АдресХранилища = ПоместитьВоВременноеХранилище(СопоставленныеДанные, АдресХранилища);
	
КонецПроцедуры

Процедура УстановитьПроцентПрогресса(Всего, НомерСтроки)
	Процент = НомерСтроки * 50 / Всего;
	МодульДлительныеОперации = ОбщегоНазначения.ОбщийМодуль("ДлительныеОперации");
	МодульДлительныеОперации.СообщитьПрогресс(Процент);
КонецПроцедуры

// Формирует отчет о загрузке в фоне 
//
Процедура СформироватьОтчетОЗагрузкеФон(ПараметрыВыгрузки, АдресХранилища) Экспорт
	
	ТаблицаОтчет = ПараметрыВыгрузки.ТаблицаОтчет;
	СопоставленныеДанные  = ПараметрыВыгрузки.СопоставленныеДанные;
	ИнформацияПоКолонкам  = ПараметрыВыгрузки.ИнформацияПоКолонкам;
	ШаблонСДанными = ПараметрыВыгрузки.ШаблонСДанными;
	ТипОтчета = ПараметрыВыгрузки.ТипОтчета;
	РассчитыватьПроцентПрогресса = ПараметрыВыгрузки.РассчитыватьПроцентПрогресса;
	
	Если Не ЗначениеЗаполнено(ТипОтчета) Тогда
		ТипОтчета = "ВсеЭлементы";
	КонецЕсли;
	
	СформироватьМакетОтчета(ТаблицаОтчет, ШаблонСДанными);
	
	ОбластьЗаголовка = ОбластьЗаголовкаШаблонаТаблицы(ТаблицаОтчет);
	
	КоличествоСозданных = 0;
	КоличествоОбновленных = 0;
	КоличествоПропущенных = 0;
	КоличествоПропущенныхСОшибкой = 0;
	Для НомерСтроки = 1 По СопоставленныеДанные.Количество() Цикл
		Строка = СопоставленныеДанные.Получить(НомерСтроки - 1);
		
		Ячейка = ТаблицаОтчет.ПолучитьОбласть(НомерСтроки + 1, 1, НомерСтроки + 1, 1);
		Ячейка.ТекущаяОбласть.Текст = Строка.РезультатСопоставленияСтроки;
		Ячейка.ТекущаяОбласть.Расшифровка = Строка.ОбъектСопоставления;
		Ячейка.ТекущаяОбласть.Примечание.Текст = Строка.ОписаниеОшибки;
		Если Строка.РезультатСопоставленияСтроки = "Создан" Тогда 
			Ячейка.ТекущаяОбласть.ЦветТекста = ЦветаСтиля.РезультатУспехЦвет;
			КоличествоСозданных = КоличествоСозданных + 1;
		ИначеЕсли Строка.РезультатСопоставленияСтроки = "Обновлен" Тогда
			Ячейка.ТекущаяОбласть.ЦветТекста = ЦветаСтиля.ИзмененноеЗначениеРеквизитаЦвет;
			КоличествоОбновленных = КоличествоОбновленных + 1;
		Иначе
			Ячейка.ТекущаяОбласть.ЦветТекста = ЦветаСтиля.ФайлЗанятыйДругимПользователем;
			КоличествоПропущенных = КоличествоПропущенных + 1;
			Если ЗначениеЗаполнено(Строка.ОписаниеОшибки) Тогда
				КоличествоПропущенныхСОшибкой = КоличествоПропущенныхСОшибкой + 1;
			КонецЕсли;
		КонецЕсли;
		
		Если ТипОтчета = "Новые" И Строка.РезультатСопоставленияСтроки <> "Создан" Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТипОтчета = "Обновленные" И Строка.РезультатСопоставленияСтроки <> "Обновлен" Тогда 
			Продолжить;
		КонецЕсли;
		
		Если ТипОтчета = "Пропущенные" И Строка.РезультатСопоставленияСтроки <> "Пропущен" Тогда 
			Продолжить;
		КонецЕсли;
		
		ТаблицаОтчет.Вывести(Ячейка);
		Для Индекс = 1 По ИнформацияПоКолонкам.Количество() Цикл 
			Ячейка = ТаблицаОтчет.ПолучитьОбласть(НомерСтроки + 1, Индекс + 1, НомерСтроки + 1, Индекс + 1);
			
			Отбор = Новый Структура("Позиция", Индекс);
			НайденныеКолонки = ИнформацияПоКолонкам.НайтиСтроки(Отбор);
			Если НайденныеКолонки.Количество() > 0 Тогда 
				ИмяКолонки = НайденныеКолонки[0].ИмяКолонки;
				Ячейка.ТекущаяОбласть.Расшифровка = Строка.ОбъектСопоставления;
				Ячейка.ТекущаяОбласть.Текст = Строка[ИмяКолонки];
				Ячейка.ТекущаяОбласть.РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Обрезать;
			КонецЕсли;
			ТаблицаОтчет.Присоединить(Ячейка);
			
		КонецЦикла;
		
		Если РассчитыватьПроцентПрогресса Тогда 
			Процент = Окр(НомерСтроки * 50 / СопоставленныеДанные.Количество()) + 50;
			МодульДлительныеОперации = ОбщегоНазначения.ОбщийМодуль("ДлительныеОперации");
			МодульДлительныеОперации.СообщитьПрогресс(Процент);
		КонецЕсли;
		
	КонецЦикла;
	
	Результат = Новый Структура;
	Результат.Вставить("ТипОтчета", ТипОтчета);
	Результат.Вставить("Всего", СопоставленныеДанные.Количество());
	Результат.Вставить("Создано", КоличествоСозданных);
	Результат.Вставить("Обновлено", КоличествоОбновленных);
	Результат.Вставить("Пропущено", КоличествоПропущенных);
	Результат.Вставить("Некорректных", КоличествоПропущенныхСОшибкой);
	Результат.Вставить("ТаблицаОтчет", ТаблицаОтчет);
	
	АдресХранилища = ПоместитьВоВременноеХранилище(Результат, АдресХранилища); 
	
КонецПроцедуры

Процедура СформироватьМакетОтчета(ТаблицаОтчет, ШаблонСДанными)
	
	ТаблицаОтчет.Очистить();
	Ячейка = ШаблонСДанными.ПолучитьОбласть(1, 1, 1, 1);
	
	ШапкаТаблицы = ШаблонСДанными.ПолучитьОбласть("R1");
	ЗаполнитьЯчейкуЗаголовкаМакета(Ячейка, НСтр("ru ='Результат'"), 12, НСтр("ru ='Результат загрузки данных'"), Истина);
	ТаблицаОтчет.Присоединить(ШапкаТаблицы); 
	ТаблицаОтчет.ВставитьОбласть(Ячейка.ТекущаяОбласть, ТаблицаОтчет.Область("C1"), ТипСмещенияТабличногоДокумента.ПоГоризонтали);
	
	ТаблицаОтчет.ФиксацияСверху = 1;
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
