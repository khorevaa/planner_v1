
//Обработчики событий
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ТекстДиалогаВыбораЦвета = ОбработкаОбъект.ПолучитьМакет("ТекстДиалогаВыбораЦвета").ПолучитьТекст();

	ТекущийЦвет = Параметры.ТекущийЦвет;

	Если ТипЗнч(ТекущийЦвет) = Тип("Цвет") Тогда

        ЦветHEX		= ПланировщикКлиентСервер.ЦветВHex(ТекущийЦвет.Красный, ТекущийЦвет.Зеленый, ТекущийЦвет.Синий);
        Красный     = ТекущийЦвет.Красный;
        Зеленый     = ТекущийЦвет.Зеленый;
        Синий       = ТекущийЦвет.Синий;

        ТекстДиалогаВыбораЦвета = СтрЗаменить(ТекстДиалогаВыбораЦвета, "name='color' value='#000000'", "name='color' value='" + ЦветHEX + "'");
        ТекстДиалогаВыбораЦвета = СтрЗаменить(ТекстДиалогаВыбораЦвета, "name='colorR' value='0'", "name='colorR' value='" + Строка(Красный) + "'");
        ТекстДиалогаВыбораЦвета = СтрЗаменить(ТекстДиалогаВыбораЦвета, "name='colorG' value='0'", "name='colorG' value='" + Строка(Зеленый) + "'");
        ТекстДиалогаВыбораЦвета = СтрЗаменить(ТекстДиалогаВыбораЦвета, "name='colorB' value='0'", "name='colorB' value='" + Строка(Синий) + "'");

    КонецЕсли;

    Объект.ДиалогВыбораЦвета = ТекстДиалогаВыбораЦвета;
	
КонецПроцедуры

//Обработчики команд
&НаКлиенте
Процедура ДиалогВыбораЦветаПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	СоставляющиеВыбранногоЦвета = Элемент.Документ;
	
	Если СоставляющиеВыбранногоЦвета = Неопределено Тогда Возврат; КонецЕсли;
	
	colorR		= СоставляющиеВыбранногоЦвета.getElementById("colorR");
	colorG		= СоставляющиеВыбранногоЦвета.getElementById("colorG");
	colorB		= СоставляющиеВыбранногоЦвета.getElementById("colorB");
	
	Красный 	= ?(colorR = Неопределено, 0, colorR.Value);
	Зеленый 	= ?(colorG = Неопределено, 0, colorG.Value);
	Синий 		= ?(colorB = Неопределено, 0, colorB.Value);
	
	Объект.ВыбранныйЦвет = Новый Цвет(Красный, Зеленый, Синий);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Закрыть(Объект.ВыбранныйЦвет);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры
