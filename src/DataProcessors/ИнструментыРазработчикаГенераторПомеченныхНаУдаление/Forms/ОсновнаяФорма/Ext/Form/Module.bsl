#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЛинейнаяЗависимостьИспользование = Истина;
	ЛинейнаяЗависимостьДлина = 5;
	ЛинейнаяЗависимостьПрефикс = НСтр("ru = 'Тест удаления: Линейные зависимости: № %1 (-> %2)'");
	
	РешаемаяКольцеваяЗависимостьИспользование = Истина;
	РешаемаяКольцеваяЗависимостьДлина = 10;
	РешаемаяКольцеваяЗависимостьПрефикс = НСтр("ru = 'Тест удаления: Кольцевые зависимости: Разрешимые: № %1 (-> %2)'");
	
	НеРешаемаяКольцеваяЗависимостьИспользование = Истина;
	НеРешаемаяКольцеваяЗависимостьДлина = 10;
	НеРешаемаяКольцеваяЗависимостьПрефикс = НСтр("ru = 'Тест удаления: Кольцевые зависимости: Не разрешимые: № %1 (-> %2)'");
	
	ПомеченныйНаУдалениеВыбранВКонстантеИспользование = Истина;
	ПомеченныйНаУдалениеВыбранВКонстантеПрефикс = НСтр("ru = 'Тест удаления: Организация выбрана в константе'");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Пуск(Команда)
	ПускНаСервере();
	ОповеститьОбИзменении(Тип("СправочникСсылка._ДемоНоменклатура"));
	ОткрытьФорму("Справочник._ДемоНоменклатура.ФормаСписка");
	ОткрытьФорму("Обработка.УдалениеПомеченныхОбъектов.Форма", , , , ВариантОткрытияОкна.ОтдельноеОкно, , , РежимОткрытияОкнаФормы.Независимый);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПускНаСервере()
	ПомечаемыеНаУдаление = Новый Массив;
	СправочникМенеджер = Справочники._ДемоНоменклатура;
	ГСЧ = Новый ГенераторСлучайныхЧисел;
	
	ПрепятствующийУдалениюНаименование = НСтр("ru = 'Тест удаления: Объект, не помеченный на удаление'");
	ПрепятствующийУдалениюСсылка = СправочникМенеджер.НайтиПоНаименованию(ПрепятствующийУдалениюНаименование);
	Если ЗначениеЗаполнено(ПрепятствующийУдалениюСсылка) Тогда
		ПрепятствующийУдалениюОбъект = ПрепятствующийУдалениюСсылка.ПолучитьОбъект();
	Иначе
		ПрепятствующийУдалениюОбъект = СправочникМенеджер.СоздатьЭлемент();
		ПрепятствующийУдалениюОбъект.Наименование = ПрепятствующийУдалениюНаименование;
		ПрепятствующийУдалениюОбъект.Записать();
		ПрепятствующийУдалениюСсылка = ПрепятствующийУдалениюОбъект.Ссылка;
	КонецЕсли;
	
	// Разрешимая цепочка.
	Если ЛинейнаяЗависимостьИспользование Тогда
		ШаблонНаименования = ЛинейнаяЗависимостьПрефикс;
		ПоследняяСсылка = Неопределено;
		Для НомерЭлемента = 1 По ЛинейнаяЗависимостьДлина Цикл
			СправочникОбъект = СправочникМенеджер.СоздатьЭлемент();
			СправочникОбъект.Наименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонНаименования,
				Формат(НомерЭлемента, "ЧГ="),
				?(НомерЭлемента = 1, "-", "" + Формат(НомерЭлемента-1, "ЧГ=")));
			СправочникОбъект.Аналоги.Добавить().Аналог = ПоследняяСсылка;
			СправочникОбъект.Записать();
			ПоследняяСсылка = СправочникОбъект.Ссылка;
			ПомечаемыеНаУдаление.Добавить(СправочникОбъект);
		КонецЦикла;
	КонецЕсли;
	
	Для ВариантЦепочки = 1 По 2 Цикл
		// 1 - Разрешимая цепочка.
		// 2 - Неразрешимая цепочка.
		Если ВариантЦепочки = 1 Тогда
			Если Не РешаемаяКольцеваяЗависимостьИспользование Тогда
				Продолжить;
			КонецЕсли;
			ШаблонНаименования = РешаемаяКольцеваяЗависимостьПрефикс;
			ДлинаКольца = РешаемаяКольцеваяЗависимостьДлина;
			НомерСлучайногоОбъекта = 0;
		Иначе
			Если Не НеРешаемаяКольцеваяЗависимостьИспользование Тогда
				Продолжить;
			КонецЕсли;
			ШаблонНаименования = НеРешаемаяКольцеваяЗависимостьПрефикс;
			ДлинаКольца = НеРешаемаяКольцеваяЗависимостьДлина;
			НомерСлучайногоОбъекта = ГСЧ.СлучайноеЧисло(1, ДлинаКольца);
		КонецЕсли;
		
		СлучайныйОбъект = Неопределено;
		ПервыйОбъект = Неопределено;
		ПоследняяСсылка = Неопределено;
		Для НомерЭлемента = 1 По ДлинаКольца Цикл
			СправочникОбъект = СправочникМенеджер.СоздатьЭлемент();
			СправочникОбъект.Наименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонНаименования,
				Формат(НомерЭлемента, "ЧГ="),
				"" + Формат(?(НомерЭлемента = 1, ДлинаКольца, НомерЭлемента-1), "ЧГ="));
			СправочникОбъект.Аналоги.Добавить().Аналог = ПоследняяСсылка;
			СправочникОбъект.Записать();
			ПоследняяСсылка = СправочникОбъект.Ссылка;
			Если НомерЭлемента = 1 Тогда
				ПервыйОбъект = СправочникОбъект;
			КонецЕсли;
			Если НомерЭлемента = НомерСлучайногоОбъекта Тогда
				СлучайныйОбъект = СправочникОбъект;
			КонецЕсли;
			ПомечаемыеНаУдаление.Добавить(СправочникОбъект);
		КонецЦикла;
		ПервыйОбъект.Аналоги.Добавить().Аналог = ПоследняяСсылка;
		ПервыйОбъект.Записать();
		
		Если ВариантЦепочки = 2 Тогда
			ПрепятствующийУдалениюОбъект.Аналоги.Добавить().Аналог = СлучайныйОбъект.Ссылка;
			ПрепятствующийУдалениюОбъект.Записать();
			СлучайныйОбъект.Наименование = СлучайныйОбъект.Наименование + ", " + НСтр("ru = 'использован в не помеченном объекте'");
			СлучайныйОбъект.Записать();
		КонецЕсли;
	КонецЦикла;
	
	Если ПомеченныйНаУдалениеВыбранВКонстантеИспользование Тогда
		ОрганизацияСсылка = Справочники.Организации.НайтиПоНаименованию(ПомеченныйНаУдалениеВыбранВКонстантеПрефикс);
		Если ЗначениеЗаполнено(ОрганизацияСсылка) Тогда
			СправочникОбъект = ОрганизацияСсылка.ПолучитьОбъект();
		Иначе
			СправочникОбъект = Справочники.Организации.СоздатьЭлемент();
			СправочникОбъект.Наименование = ПомеченныйНаУдалениеВыбранВКонстантеПрефикс;
			СправочникОбъект.Записать();
			ОрганизацияСсылка = СправочникОбъект.Ссылка;
		КонецЕсли;
		Константы._ДемоОсновнаяОрганизация.Установить(ОрганизацияСсылка);
		ПомечаемыеНаУдаление.Добавить(СправочникОбъект);
	КонецЕсли;
	
	// Установка пометок удаления в случайном порядке.
	ОсталосьПометить = ПомечаемыеНаУдаление.Количество();
	ГСЧ = Новый ГенераторСлучайныхЧисел;
	Пока ОсталосьПометить > 0 Цикл
		ОсталосьПометить = ОсталосьПометить - 1;
		Индекс = ГСЧ.СлучайноеЧисло(0, ОсталосьПометить);
		ПомечаемыеНаУдаление[Индекс].УстановитьПометкуУдаления(Истина);
		ПомечаемыеНаУдаление.Удалить(Индекс);
	КонецЦикла;
КонецПроцедуры

#КонецОбласти
