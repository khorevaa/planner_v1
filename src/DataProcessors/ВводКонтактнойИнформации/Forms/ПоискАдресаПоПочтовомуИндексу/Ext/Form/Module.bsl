// Параметры формы:
//     Индекс                     - Число  - Почтовый индекс для поиска вариантов адресов
//     СкрыватьНеактуальныеАдреса - Булево - флаг того, что при неактуальные адреса будут скрываться
//
// Результат выбора:
//     Структура - с полями:
//         * Код - Число - Данные адреса
//         * Представление - Данные адреса
//
// ---------------------------------------------------------------------------------------------------------------------
//
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Индекс = Параметры.Индекс;
	ПараметрыПоиска = Новый Структура("СкрыватьНеактуальные", Параметры.СкрыватьНеактуальныеАдреса);
	ДанныеКлассификатора = КонтактнаяИнформацияСлужебный.АдресаКлассификатораПоИндексу(Индекс, ПараметрыПоиска);
	
	Если ДанныеКлассификатора.Данные.Количество() = 0 Тогда
		// Нет данных, функционал выбора не применим
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ВариантыАдреса.Загрузить(ДанныеКлассификатора.Данные);
	
	ОбщаяЧастьПредставления = ДанныеКлассификатора.ОбщаяЧастьПредставления;
	ОбщаяЧастьПредставления = Индекс + ?(ПустаяСтрока(ОбщаяЧастьПредставления), "", ", ") + ОбщаяЧастьПредставления;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура ВариантыАдресаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ПроизвестиВыбор(ВыбраннаяСтрока);
КонецПроцедуры

&НаКлиенте
Процедура ВариантыАдресаВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	ПроизвестиВыбор(Значение);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПроизвестиВыбор(Знач НомерСтроки)
	
	Данные = ВариантыАдреса.НайтиПоИдентификатору(НомерСтроки);
	Если Данные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Данные.Неактуален Тогда
		ПередатьДанныеВыбораВладельцу(Данные);
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(ОбщаяЧастьПредставления) Тогда
		ТекстВопроса = НСтр("ru = 'Адрес ""%1"" неактуален.
		                          |Продолжить?'");
	Иначе
		ТекстВопроса = НСтр("ru = 'Адрес ""%2, %1"" неактуален.
		                          |Продолжить?'");
	КонецЕсли;
	
	ПредупреждениеНеактуальности = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ТекстВопроса, Данные.Представление, ОбщаяЧастьПредставления);
		
	ЗаголовокПредупреждения = НСтр("ru = 'Подтверждение'");
	
	Оповещение = Новый ОписаниеОповещения("ПроизвестиВыборЗавершениеВопроса", ЭтотОбъект, Данные);
	ПоказатьВопрос(Оповещение, ПредупреждениеНеактуальности, РежимДиалогаВопрос.ДаНет, , ,ЗаголовокПредупреждения);
		
КонецПроцедуры

&НаКлиенте
Процедура ПроизвестиВыборЗавершениеВопроса(Знач РезультатВопроса, Знач ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ПередатьДанныеВыбораВладельцу(ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередатьДанныеВыбораВладельцу(Знач Данные)
	Результат = Новый Структура("Индекс, Код, Представление");
	
	ЗаполнитьЗначенияСвойств(Результат, Данные);
	Результат.Индекс = Индекс;
	
	ОповеститьОВыборе(Результат);
КонецПроцедуры

#КонецОбласти
