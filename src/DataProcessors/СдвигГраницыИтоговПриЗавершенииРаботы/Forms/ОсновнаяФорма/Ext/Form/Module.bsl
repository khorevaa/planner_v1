
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Параметры.Свойство("ОткрытаПоСценарию") Тогда
		ВызватьИсключение НСтр("ru='Обработка не предназначена для непосредственного использования.'");
	КонецЕсли;
	
	Если НЕ УправлениеИтогамиИАгрегатамиСлужебный.НадоСдвинутьГраницуИтогов() Тогда
		Отказ = Истина; // Период уже был установлен в сеансе другого пользователя.
		Возврат;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПодключитьОбработчикОжидания("УстановитьПериодРассчитанныхИтогов", 0.1, Истина);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьПериодРассчитанныхИтогов()
	ВремяНачала = ОбщегоНазначенияКлиент.ДатаСеанса();
	УстановитьПериодРассчитанныхИтоговНаСервереБезКонтекста();
	СкоростьВыполненияВСекундах = ОбщегоНазначенияКлиент.ДатаСеанса() - ВремяНачала;
	Если СкоростьВыполненияВСекундах >= 5 Тогда
		Закрыть();
	Иначе
		ПодключитьОбработчикОжидания("ЗакрытьФорму", 5 - СкоростьВыполненияВСекундах, Истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму()
	Закрыть();
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьПериодРассчитанныхИтоговНаСервереБезКонтекста()
	УстановитьПривилегированныйРежим(Истина);
	УправлениеИтогамиИАгрегатамиСлужебный.УстановитьПериодРассчитанныхИтогов();
КонецПроцедуры

#КонецОбласти
